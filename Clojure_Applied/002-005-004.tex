\section{プロセスで考える}

これまで、一回限りの非同期（\texttt{future}や\texttt{promise}）、粗粒度のタスク並列、細粒度のデータ並列のためのコアの使い方をみてきました。しかし、時には並列性よりも並行性に興味を持つことがあります。つまり、プログラムを一連の同時実行スレッドとして設計する能力です。また、これらのスレッド間で、一度だけでなく、時間の経過に伴う一連の値として、値を伝達したいと思うこともあるはずだ。

\texttt{core.async}ライブラリは、このようなニーズに対する回答として作成された。当初はClojure自体の一部として構想されましたが、最終的にはコア言語よりも迅速な進化を可能にするために、独立したライブラリとしてリリースされました。\texttt{core.async}ライブラリは、goブロック（独立した実行スレッド）とチャネル（ある場所から別の場所に値を渡す手段）という2つの中心的な概念を提供します。次に、これらの使い方を探ります。


\subsection{チャネル}

チャネルは、プログラムの2つ以上の部分の間で一連の値を時間的に伝達するための待ち行列のような手段です。チャネルは、スレッド間で作成したり受け渡ししたりすることができます - ステートフルです。

チャネルは、チャネル内の値を保持するためにバッファを使用します。デフォルトでは、チャネルはバッファなし（長さゼロ）で、Javaの\texttt{SynchronousQueue}に似ています。バッファされていないチャネルは、producer と consumer の両方がチャネルに値を渡すことができるようになるまでブロックされます。\texttt{core.async} ライブラリは，固定長のバッファ，ドロップバッファ（新しいデータが一杯になったらドロップする），スライディングバッファ（古いデータが一杯になったらドロップする）も提供します．

\texttt{core.async} でチャネルを作成するには、\texttt{chan} 関数を使用します。以下は、異なるバッファタイプとサイズのチャネルを作成する例です。

\begin{lstlisting}[numbers=none]
(require '[clojure.core.async :refer
 (chan dropping-buffer sliding- buffer)])

(chan) ;; unbuffered (length=0)
(chan 10) ;; buffered (length=10)
(chan (dropping-buffer 10)) ;; drop new values when full
(chan (sliding-buffer 10)) ;; drop old values when full
\end{lstlisting}

どんな典型的なClojure値でもチャネルに入れることができ、相手側に伝達されます。1つの例外は \texttt{nil} で、これはチャネルが閉じられ、それ以上データが残っていないことを示すために使用される特別な値です。チャネルは，\texttt{close!}関数で閉じられます．

チャネルに対する最も重要な操作は \texttt{put} と \texttt{take} の 2 つであり、それぞれコンテキストや用途に応じていくつかの形式があります。通常のスレッドからチャネルを使用する場合、\texttt{put} 演算子は \texttt{>!!}、\texttt{take} 演算子は \texttt{<!!} となります。以下は，チャネルを作成し，そこに値を入れ，取り出す例です．

\begin{lstlisting}[numbers=none]
(require '[clojure.core.async :refer (chan <!! >!!)])

(def c (chan 1))
(>!! c "hello")
(println (<!! c))
\end{lstlisting}

前述のコード例では、\texttt{put}と\texttt{take}の両方の操作を現在のスレッドから行っていますが、実際のプログラムでは、チャネルの両端は通常、異なるスレッドまたはコンポーネントから使用され、それらの間で値が送信されるようになっています。また、サイズ 1 のバッファを持つチャネルを作成したことにお気づきでしょうか。もしバッファのないチャンネルを使っていたら、この例では put の待ち時間がブロックされ、この例が完了するのを妨げていたでしょう。

\texttt{core.async} のチャネルは決して unbounded ではありません。これは、後で問題になるようなアーキテクチャ上の問題を避けるための、意図的な設計上の制約です。システム内の未束縛のキューは、予期せぬ負荷が積み重なり、最終的にはリソースを使い果たし、システムをクラッシュさせる場所となります。

その代わりに、\texttt{core.async}では、固定サイズを選択するか、バッファがいっぱいになったときに何をドロップするかのポリシーをインスタンス化することで、バッファの長さを制限することを要求します。固定サイズのバッファは、満杯のキューに追加しようとするとプロデューサーをブロックさせるので、バックプレッシャーを発生させます。これは、実稼働時ではなく、前もっての設計思考を促すものです。システムは、負荷がなくなるのを待つか、仕事を減らすことを受け入れるか、どの仕事をしないかを選択することによって、明示的にブロックに対処するように設計されていなければなりません。

チャネルを別のサブシステムの専用スレッドから完全に使用することも可能ですが、Goブロックから使用するのが一般的です。Goブロックを使うと、スレッドのプールで対応できる軽量な処理ループを作ることができます。

\subsection{Goブロック}

伝統的に、Java（またはClojure）プログラムは、プログラムの各部分の実際の処理を含むスレッド（実際のオペレーティング・システムのスレッドに対応する）を作成します。\texttt{core.async}ライブラリは、C. A. R. HoareのCommunicating Sequential Processes (CSP) [Hoa78]の遺産に基づく、異なる伝統に従います。

この研究の詳細については、ここでは触れません。重要なことは、プログラムをどのように構成するかについて、異なる方法で考えることを学ぶことです。スレッドは希少で高価な資源です。スタックスペースやその他の資源を消費し、起動も比較的遅いです。スレッドがI/Oのためにブロックされると、これらのシステムリソースを浪費することになります。

その代わりに、\texttt{core.async}は、スレッドプールにマッピングされ、作業の準備ができたときにだけ実行される軽量なプロセスという観点から考えることを推奨しています。チャネルへのメッセージの出入りを待つ間にブロックする代わりに、プロセスが再び実行できるようになるまで、これらのプロセスを停止させることができます。これによって、やるべきことがあるときだけプロセスを実行することができる。また、複数の I/O 操作にまたがって選択し、最初の操作が完了したときに処理を進めるという、興味深い新しい動作を実装することも可能です。

\texttt{core.async}では、これらのプロセスをGoブロックと呼んでいます（Go言語の同様の概念にちなんでいます）。goブロックの中ではチャネルを使いますが、\texttt{put}と\texttt{take}の操作は \texttt{<!} と \texttt{>!} です。

以下は、メッセージを受信し、それを表示する処理を行うgoブロックを作成する関数の例です。

\begin{lstlisting}[numbers=none]
(require '[clojure.core.async :refer (go <!)])

(defn go-print
  "チャネル c からメッセージを取り出し、表示する。"
  [c]
  (go
    (loop []
      (when-some [val (<! c)]
        (println "Received a message:" val)
        (recur)))))
\end{lstlisting}

この例では、goブロックは軽量なプロセスとして実行されます。チャンネル操作（\texttt{<!} や \texttt{>!} など）に到達したとき、そのチャンネル操作が可能であれば実行を継続する。チャンネル操作が継続できない場合、go ブロックはパークされます。パークされたgoブロックはスレッドを消費せず、事実上、データを待つために中断された計算となります。チャネル操作が続行できるようになると、goブロックは実行を継続するために起動します。

Goブロックは、プログラムを潜在的な同時処理に分割するための素晴らしいツールです。\texttt{core.async}がgoブロックとチャネルで特によくサポートする使用例は、データ変換ステージのパイプラインを構築することです。

\subsection{パイプライン}

\texttt{core.async}ライブラリは，2つのチャンネルを並列変換ステージで接続するための関数群，すなわち\texttt{pipeline}，\texttt{pipeline-blocking}，\texttt{pipeline-async}を提供します．このパイプライン関数は、入力チャネルから出力チャネルに値を移動しますが（より単純な\texttt{pipe}と同様）、重要な追加機能を提供します：トランスデューサの並列実行です。

この機能により、パイプラインはチャネルで区切られたデータ変換ステージを作成するのに適しています。並列実行により、データパイプラインを線形に記述しながら、コアをフル活用することができます。各トランスデューサステージは多くの変換を組み合わせることができるため、どのように並列化するか、どこでチャネルを分離するのが有効か、多くの選択肢があります。

例えば、ソーシャルメディアのメッセージのストリームを処理するシステムを考えてみましょう。トランスデューサーとして定義された一連の変換を提供することができます。

\begin{lstlisting}[numbers=none]
;; メッセージを単語の集合に解析する
(def parse-words
  (map #(set (clojure.string/split % #"\s"))))
;; 単語を含むメッセージをフィルタリングする
(def interesting (filter #(contains? % "Clojure")))
;; 異なる単語リストに基づいて感情を検出
(defn match [search-words message-words]
  (count (clojure.set/intersection search-words
                                   message-words)))
(def happy
  (partial match
           #{"happy" "awesome" "rocks" "amazing"}))
(def sad (partial match #{"sad" "bug" "crash"}))
(def score (map #(hash-map :words %1
                           :happy (happy %1)
                           :sad (sad %1))))
\end{lstlisting}

これらのトランスデューサは、入力ストリームから出力ストリームまで1段のパイプラインで一緒に構成することができます。


\begin{lstlisting}[numbers=none]
(defn sentiment-stage
  [in out]
  (let [xf (comp parse-words interesting score)]
    (async/pipeline 4 out xf in)))
\end{lstlisting}

これは、最大4つの並列スレッドで \texttt{in} と \texttt{out} を接続し、それぞれが結合されたトランスデューサの変換を処理します。しかし、センチメント分析が行われている間に、アーカイブへのロギングなど、別のパイプラインステージで発生する可能性のある他の分析があるかもしれません。その場合、このステージを2つに分割し、センチメント解析の前に新しい中間チャネルを作成することができます。

\begin{lstlisting}[numbers=none]
(defn interesting-stage
  [in intermediate]
  (let [xf (comp parse-words interesting)]
    (async/pipeline 4 intermediate xf in)))

(defn score-stage
  [intermediate out]
  (async/pipeline 1 out score intermediate))

(defn assemble-stages
  [in out]
  (let [intermediate (async/chan 100)]
    (interesting-stage in intermediate)
    (score-stage intermediate out)))
\end{lstlisting}

現在では、受信したメッセージをすべて受け取り、興味深いものだけを出力する第一ステージ（最大4スレッドを使用）と、興味深いメッセージを受け取り、スコアをつける第二ステージがあります。量が少ないので、第2ステージの並列度を1スレッドに減らすことができます。これらのステージを組み立てると、中間メッセージチャネルを他の目的に使用する機会も得られます。

トランスデューサーはコンポーザブルなので、1つのステージに積み重ねることもできますし、ステージをまたいで分割することも可能です。各ステージの並列度は独立して変化させることができる。これは、効率的なデータ処理パイプラインを構築するための強力な技術です。パイプラインは、きめ細かなデータ並列処理の性能を犠牲にすることになりますが、より柔軟なアーキテクチャを実現することができます。


