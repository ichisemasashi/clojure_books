\section{物事をまとめる}

Componentでは、システムは他のコンポーネントを起動したり停止したりできる特別なコンポーネントである。システム内のコンポーネントは、依存関係が常にコンポーネントの前に開始されるような順序で開始される。このため、コンポーネントの依存関係グラフにサイクルが存在しないことが必要である。同様に、システムが停止されるとき、コンポーネントは開始と逆の順序で停止される。

システムは、\texttt{component/system-map}関数で定義される。マップは、コンポーネント名からコンポーネントインスタンスへのマッピングを定義する。コンポーネントがコンポーネントの依存関係を持つ場合、これは \texttt{component/using} で指定され、注入されたコンポーネントのベクトル（システム内とコンポーネント内で名前が同じ場合）またはコンポーネント名からシステム名へのマッピングのいずれかを取ります。

この関数は、システムで定義したコンポーネントから、コンポーネントシステムマップを作成します。

\begin{lstlisting}[numbers=none]
(defn system [{:keys (twitter facebook knowledge approvals) :as config}]
  (let [twitter-chan (async/chan 100)
        twitter-response-chan (async/chan 10)
        facebook-chan (async/chan 100)
        facebook-response-chan (async/chan 10)
        alert-chan (async/chan 100)
        response-chan (async/chan 100)
        feed-chan (async/merge [twitter-chan facebook-chan])
        response-pub (async/pub response-chan :feed)]
    (async/sub response-pub :twitter twitter-response-chan)
    (async/sub response-pub :facebook facebook-response-chan)
    (component/system-map
      :twitter (feed/new-feed twitter twitter-chan twitter-response-chan)
      :facebook (feed/new-feed facebook facebook-chan facebook-response-chan)
      :knowledge-engine
        (kengine/new-knowledge-engine knowledge feed-chan alert-chan)
      :approvals (component/using
                   (approvals/new-approvals approvals alert-chan response-chan)
                   [:knowledge-engine]))))
\end{lstlisting}

\texttt{system} 関数の最初の部分では、システムに必要な \texttt{core.async} チャネルとその他のチャネルパイプをすべて作成します。次に \texttt{component/system-map} 関数で、4つのコンポーネントとその構成、そしてナレッジエンジンを承認コンポーネントに注入する方法を定義しています。最後に、システムマップと承認コンポーネント自体で名前が一致する1つのコンポーネントのベクトルで、 \texttt{component/using} の使い方を示しています。

各コンポーネントには、コンポーネントを起動したり、その動作に影響を与えるために必要な設定情報があります。ほとんどのアプリケーションは、多くの構成要素を持つことになります。次に、開発から配備までの完全なライフサイクルをサポートする方法で、アプリケーションに設定データをロードする方法を見てみましょう。







