

Chapter 8


# マクロの書き方

18歳のとき、私はニューメキシコ州サンタフェのホテルで夜間監査員の仕事に就きました。週4日、午後11時から午前7時まで働きました。18歳の時、ニューメキシコ州サンタフェのホテルでナイト・オーディターの仕事に就いた。

ある夜、午前3時ごろに、男性の髪を修復するという商品のコマーシャルを見ていました。ハゲていた人の話を聞きながら、私は心から喜びを感じた。「ついに！」私の脳は湧き上がった。この人は、自分にふさわしい愛と成功を手に入れたんだ！」。絶望している人に希望を与えるなんて、なんてすごい商品なんだ！" と。

それ以来、私は、慢性的な睡眠不足によって引き起こされた感情の開放と人生への感謝を、どうにかして再現できないものかと考えている。ある種の薬、おそらく---私の内なるリチャード・シモンズを解き放つために2、3杯は飲むだろう、しかし、あまり長くは続かないだろう。

![](simmons-potion.png)

ポーションで一時的に自分の根本的な性質を変えることができるように、マクロを使えば他の言語では不可能な方法でClojureを変更することができます。マクロを使えば、あなたの問題空間に合わせてClojureを拡張し、言語を構築することができます。

この章では、マクロの書き方を徹底的に調べ、基本的な例から始めて、複雑さを増していきます。最後に、架空のオンラインポーションストアで顧客の注文を検証するために、作り物の帽子をかぶり、マクロを使用することになります。

この章の終わりには、マクロを書くために使うすべてのツール、quote、syntax quote、unquote、unquote splicing（別名piñata tool）、gensymを理解することができるようになります。また、二重評価、変数キャプチャ、マクロ感染など、無防備なマクロ作者を待ち受けている危険についても学びます。

## マクロは必要不可欠

マクロを書き始める前に、マクロを適切な文脈で理解する手助けをしたいと思います。確かにマクロは白熊の爪よりカッコイイですが、マクロをコードで特別に凝りたいときに取り出す難解な道具と考えるべきではありません。実際、マクロはClojureが関数と特別なフォームの小さなコアからその組み込み機能の多くを導き出すことを可能にします。例えば、 `when` を見てみましょう。`when` はこのような一般的な形をしています。



```
(when boolean-expression
  expression-1
  expression-2
  expression-3
  ...
  expression-x)
```



`when` は `if` のような特別な形式だと思うかもしれません。さて、どうでしょう？そうではありません。他の多くの言語では、条件式は特別なキーワードを使用してのみ作成でき、独自の条件演算子を作成する方法はありません。しかし、`when`は実はマクロなんだ。

このマクロ展開では、`when` が `if` と `do` で実装されていることがわかる。



```
(macroexpand '(when boolean-expression
                expression-1
                expression-2
                expression-3))
; => (if boolean-expression
       (do expression-1
           expression-2
           expression-3))
```



これは、マクロがClojureの開発に不可欠なものであり、基本的な操作を提供するために使用されていることを示しています。 マクロはエキゾチックな特殊なケースに限定されるものではありません; マクロを書くことは、あなたの道具袋の中の単なる1つのツールと考えるべきです。あなた自身のマクロを書くことを学ぶと、それがあなたの特定の問題領域の形に合うように、言語をさらに拡張することを可能にすることが分かるでしょう。

## マクロの構造

マクロの定義は、関数定義とよく似ています。名前、オプションの文書文字列、引数リスト、そしてボディがあります。本体は、ほとんどの場合リストを返します。これは、マクロがデータ構造をClojureが評価できる形に変換する方法であり、Clojureが関数呼び出し、特別なフォーム呼び出し、およびマクロ呼び出しを表すためにリストを使用するため、意味があります。マクロ本体内で任意の関数、マクロ、特殊フォームを使用することができ、関数や特殊フォームと同じようにマクロを呼び出すことができます。

例として、私たちの古い友人である `infix` マクロを紹介します。



```
(defmacro infix
  "Use this macro when you pine for the notation of your childhood"
  [infixed]
  (list (second infixed) (first infixed) (last infixed)))
```



このマクロは、リストを infix 記法で正しい順序に並べ替えます。 以下はその例である。



```
(infix (1 + 1))
; => 2
```



関数とマクロの大きな違いは、関数の引数は関数に渡される前に完全に評価されるのに対し、マクロは評価されないデータとして引数を受け取ることです。このことは、例で見ることができます。もし `(1 + 1)` をそれ自体で評価しようとしたら、例外が発生するでしょう。しかし、マクロを呼び出しているので、評価されないリスト `(1 + 1)` は `infix` に渡されます。そして、マクロは `first`, `second`, `last` を使用して、Clojureが評価できるようにリストを並べ替えます。



```
(macroexpand '(infix (1 + 1)))
; => (+ 1 1)
```



マクロを展開すると、`infix` が `(1 + 1)` を `(+ 1 1)` に並べ替えていることがわかります。便利ですね!

関数と同じように、マクロの定義でも引数のデストラクションを使用することができます。



```
(defmacro infix-2
  [[operand1 op operand2]]
  (list op operand1 operand2))
```



引数の再構築を行うことで、シーケンシャルな引数の中の位置に基づいて、シンボルに値を簡潔にバインドすることができます。ここでは、 `infix-2` は引数としてシーケンシャルなデータ構造を受け取り、マクロ内で最初の値を `operand1` 、2番目の値を `op` 、3番目の値を `operand2` という名前になるように、位置によって再構築を行います。

また、複数のアリティを持つマクロを作成することができ、実際、基本的なブール演算である `and` と `or` はマクロとして定義されています。以下は `and` のソースコードです。



```
(defmacro and
  "Evaluates exprs one at a time, from left to right. If a form
  returns logical false (nil or false), and returns that value and
  doesn't evaluate any of the other expressions, otherwise it returns
  the value of the last expr. (and) returns true."
  {:added "1.0"}
  ([] true)
  ([x] x)
  ([x & next]
   `(let [and# ~x]
      (if and# (and ~@next) and#))))
```



この例では、シンボル `` ` `` や `~@` など、いろいろなものが出てきますが、これについてはすぐに学習します。常に `true` を返す 0-arity マクロボディ、オペランドを返す 1-arity マクロボディ、そして再帰的に自分自身を呼び出す *n*-arity マクロボディです。 そうです。マクロは再帰的に呼び出すことができ、関数と同じようにrest引数 (*n*-arity マクロボディの `& next`) を使用することができます。

さて、マクロの解剖学に慣れたところで、オデュッセウス風に思考マストに縛られて、マクロ本体の書き方を学びましょう。

## 評価用リストの構築

マクロを書くことは、Clojureが評価するためのリストを構築することであり、通常の考え方とは一種の逆転が必要です。1つは、最終的なリストで評価されないデータ構造を取得するために、しばしば式をクォートする必要があります(これについては、後で説明します)。より一般的には、*シンボル*とその*値*の違いについて、特に注意する必要があります。

### シンボルと値を区別する

ある式を受け取って、その値を表示して返すマクロを作りたいとします。(これは `println` と異なり、 `println` は常に `nil` を返します。) あなたのマクロは次のようなリストを返したいのです。



```
(let [result expression]
  (println result)
  result)
```



マクロの最初のバージョンは次のようになります。Clojureが評価すべきリストを作成するために `list` 関数を使用します。



```
 (defmacro my-print-whoopsie
  [expression]
  (list let [result expression]
        (list println result)
        result))
```



しかし、これを試した場合、例外 `Can't take the value of a macro: #'clojure.core/let` が発生します。ここで何が起こっているのでしょうか？

このようなことが起こる理由は、マクロ本体が `let` というシンボルが参照する *値* を取得しようとするためです。一方、実際に行いたいことは `let` シンボル自体を返すことです。他にも問題があります。束縛されていない `result` の値を取得しようとしていたり、シンボルを返す代わりに `println` の値を取得しようとしています。以下は、あなたが望むことを行うためにマクロをどのように書くかです。



```
(defmacro my-print
  [expression]
  (list 'let ['result expression]
        (list 'println 'result)
        'result))
```



ここでは、シンボルとして使用したい各シンボルを、シングルクォート文字 `'` で前置きしてクォートしています。この場合、Clojureはシンボルを解決しようとせず、代わりにシンボルを返すだけです。クォートを使って評価をオフにする機能は、マクロを書く上で中心的なものなので、このトピックに独自のセクションを設けましょう。

### 簡単なクウォート

評価されないシンボルを取得するために、マクロの中でクオートを使用することはほとんどでしょう。ここでは、クォートについて簡単に説明し、マクロの中でどのように使用するかを見ていきましょう。

まず、クオートを使用しないシンプルな関数呼び出しです。



```
(+ 1 2)
; => 3
```



先頭に `quote` を付けると、未評価のデータ構造が返される。



```
(quote (+ 1 2))
; => (+ 1 2) 
```



ここで、返されたリストの中の `+` はシンボルである。このプラス記号を評価すると、プラス関数が得られる。



```
+
; => #<core$_PLUS_ clojure.core$_PLUS_@47b36583>
```



一方、プラス記号をクォートすると、プラス記号が得られるだけである。



```
(quote +)
; => +
```



束縛されていないシンボルを評価すると、例外が発生します。



```
sweating-to-the-oldies
; => Unable to resolve symbol: sweating-to-the-oldies in this context
```



しかし、シンボルをクォートすると、そのシンボルに値が関連付けられているかどうかに関係なく、シンボルが返されます。



```
(quote sweating-to-the-oldies)
; => sweating-to-the-oldies
```



シングルクォート文字は `(quote `[x]`)` のリーダーマクロです。



```
'(+ 1 2)
; => (+ 1 2)

'dr-jekyll-and-richard-simmons
; => dr-jekyll-and-richard-simmons
```



マクロ `when` でクウォートが有効であることを確認することができます。これは `when`['] の実際のソースコードです。



```
(defmacro when
  "Evaluates test. If logical true, evaluates body in an implicit do."
  {:added "1.0"}
  [test & body]
  (list 'if test (cons 'do body)))
```



マクロの定義では、 `if` と `do` の両方をクォートしていることに注意してください。これは、 `when` が評価のために返す最終的なリストにこれらのシンボルが含まれるようにしたいからです。以下は、返されるリストがどのようなものであるかの例です。



```
(macroexpand '(when (the-cows-come :home)
                (call me :pappy)
                (slap me :silly)))
; => (if (the-cows-come :home)
       (do (call me :pappy)
           (slap me :silly)))
```



組み込みマクロのソースコードのもう一つの例です。今回は `unless` です。



```
(defmacro unless
  "Inverted 'if'"
  [test & branches]
  (conj (reverse branches) test 'if))
```



ここでも `if` をクウォートしなければならないのは、このように評価されないシンボルが結果のリストに置かれるようにしたいからです。



```
(macroexpand '(unless (done-been slapped? me)
                      (slap me :silly)
                      (say "I reckon that'll learn me")))
; => (if (done-been slapped? me)
       (say "I reckon that'll learn me")
       (slap me :silly))
```



マクロを書くときにこのような単純なクォートを使う場合も多いが、より強力な構文クォートを使う場合がほとんどである。

### 構文クォート

これまで、 `list` 関数を使って `'` (quote) と共にリストを作成するマクロや、 `first`, `second`, `last` などのリストを操作する関数を見てきたと思います。実際、この方法で牛が帰ってくるまでマクロを書くことができます。しかし、時には面倒で冗長なコードになってしまうこともあります。

構文クオートは、通常のクオートと同様に、評価されないデータ構造を返します。しかし、2つの重要な違いがあります。一つは、構文クォートは*完全修飾*された（つまり、シンボルの名前空間が含まれた）シンボルを返すという違いです。クォートと構文クォートを比較してみましょう。

クォートでは、コードに名前空間が含まれていない場合、名前空間は含まれません。



```
'+
; => +
```



名前空間を書き出せば、通常のクォートで返されます。



```
'clojure.core/+
; => clojure.core/+
```



構文クオートは、常にシンボルの完全な名前空間を含みます。



```
`+
; => clojure.core/+
```



リストのクォートは，再帰的にすべての要素をクォートします．



```
'(+ 1 2)
; => (+ 1 2)
```



リストの構文クオートは、すべての要素を再帰的に構文クオートします。



```
`(+ 1 2)
; => (clojure.core/+ 1 2)
```



構文クォートに名前空間が含まれるのは、第6章で扱う名前の衝突を回避するためです。

クォートと構文クォートのもうひとつの違いは、後者ではチルダ、 `~` を使ってフォームを *アンクォート* することができるということです。これはクリプトナイトのようなもので、スーパーマンがクリプトナイトのそばにいると、その力が消えてしまうのです。チルダが構文クォートされたフォームの中に現れると、構文クォートが評価されない、完全に名前空間化されたフォームを返すという力が消えてしまいます。以下はその例です。



```
`(+ 1 ~(inc 1))
; => (clojure.core/+ 1 2)
```



チルダの後にあるので、`(inc 1) `はクォートされる代わりに評価されます。アンクォートがない場合、構文クォートは完全修飾されたシンボルによる未評価のフォームを返します。



```
`(+ 1 (inc 1))
; => (clojure.core/+ 1 (clojure.core/inc 1))
```



文字列の補間に慣れている人は、構文クォート/アンクォートも同じように考えることができます。どちらの場合も、より大きな静的な構造の中にいくつかの変数を配置し、一種のテ ンプレートを作っていることになります。たとえば、Ruby では、文字列 `"Churn your butter, Jebediah!"` を連結して作ることができます。



```
name = "Jebediah"
"Churn your butter, " + name + "!"
```



または補間によって



```
"Churn your butter, #{name}!"
```



文字列の補間がより明確で簡潔なコードにつながるのと同じように、構文クォートと構文アンクォートによって、より明確で簡潔なリストを作成することができます。最初に示した `list` 関数を使う場合と、構文クォートを使う場合とを比較してみましょう。



```
(list '+ 1 (inc 1))
; => (+ 1 2)

`(+ 1 ~(inc 1))
; => (clojure.core/+ 1 2)
```



ご覧の通り、構文クォート版の方がより簡潔です。また、視覚的にもリストの最終形に近くなり、理解しやすくなっています。

## マクロで構文クォートを使う

さて、構文クォートがどのように機能するかについてよく理解できたと思いますので、 `code-critic` マクロをみてみましょう。構文クォートを使って、より簡潔なバージョンを書くことになります。



```
(defmacro code-critic
  "Phrases are courtesy Hermes Conrad from Futurama"
  [bad good]
  (list 'do
        (list 'println
              "Great squid of Madrid, this is bad code:"
              (list 'quote bad))
        (list 'println
              "Sweet gorilla of Manila, this is good code:"
              (list 'quote good))))

(code-critic (1 + 1) (+ 1 1))
; => Great squid of Madrid, this is bad code: (1 + 1)
; => Sweet gorilla of Manila, this is good code: (+ 1 1)
```



`list`とシングルクォートの退屈な繰り返しを見るだけで、ぞっとします。しかし、`code-critic`を構文クォートで書き直せば、スマートで簡潔なものにすることができるのです。



```
(defmacro code-critic
  "Phrases are courtesy Hermes Conrad from Futurama"
  [bad good]
  `(do (println "Great squid of Madrid, this is bad code:"
                (quote ~bad))
       (println "Sweet gorilla of Manila, this is good code:"
                (quote ~good))))
```



この場合、`good`と`bad`というシンボル以外をすべてクォートしたいことになります。オリジナルのバージョンでは、この二つのシンボルがクォートされないようにするためだけに、それぞれの部分を個別にクォートして、扱いにくい方法で明示的にリストに入れなければなりません。構文クォートを使えば、`do`式全体をクォートで囲んで、評価したい2つのシンボルをクォート解除するだけでよいのです。

これでマクロの書き方の紹介は終わりです。 西サモアと東サモアの聖なるボア、大変だったね!

要約すると、マクロは評価されない任意のデータ構造を引数として受け取り、Clojureが評価するデータ構造を返します。マクロを定義するとき、関数や`let`バインディングと同じように引数のデストラクチャリングを使用することができます。また、多重アリティや再帰的なマクロも書くことができます。

ほとんどの場合、マクロはリストを返します。リストを返すには、 `list` 関数を使用するか、構文クォートを使用します。構文クォートを使用すると、視覚的に解析しやすいデータ構造のテンプレートを作成できるため、通常、より明確で簡潔なコードを作成できます。構文クォートを使うにせよ、プレーンクォートを使うにせよ、リストを作成する際にはシンボルと評価される値の区別を明確にすることが重要です。そして、Clojureが評価するためにマクロが複数のフォームを返すようにしたい場合、それらを `do` でラップすることを確認してください。

## マクロのリファクタリングとアンクォートのスプライシング

前のセクションで紹介した `code-critic` マクロは、まだ改良の余地があります。重複している部分を見てください! 2つの `println` 呼び出しはほとんど同じです。これをきれいにしましょう。まず、これらの `println` リストを生成するための関数を作成しましょう。関数はマクロよりも考えやすく、遊びやすいので、マクロの中身をヘルパー関数に移すのはいい考えです。



```
(defn criticize-code
  [criticism code]
  `(println ~criticism (quote ~code)))

(defmacro code-critic
  [bad good]
  `(do ~(criticize-code "Cursed bacteria of Liberia, this is bad code:" bad)
       ~(criticize-code "Sweet sacred boa of Western and Eastern Samoa, this is good code:" good)))
```



`criticize-code`関数が構文クォートされたリストを返していることに注目してください。 これは、マクロが返すリストを構築する方法です。

しかし、もっと改善の余地があります。このコードではまだ、ほぼ同じ関数を複数回呼び出しています。このように、値のコレクションに同じ関数を適用したい場合、 `map` のような seq 関数を使用するのが理にかなっています。



```
(defmacro code-critic
  [bad good]
  `(do ~(map #(apply criticize-code %)
             [["Great squid of Madrid, this is bad code:" bad]
              ["Sweet gorilla of Manila, this is good code:" good]])))
```



これで少しはマシになったかな。criticism/code のペアをマッピングして、そのペアに `criticize-code`関数を適用していますね。コードを実行してみましょう。



```
(code-critic (1 + 1) (+ 1 1))
; => NullPointerException
```



あれっ、全然効いてない！？何が起こったのでしょうか？問題は `map` がリストを返すことで、この場合は `println` 式のリストが返されます。私たちはそれぞれの `println` の呼び出しの結果が欲しいだけなのに、このコードでは両方の結果をリストに格納して、そのリストを評価しようとしています。

言い換えれば、このコードを評価するとき、Clojureは次のようなものに辿り着きます。



```
(do
 ((clojure.core/println "criticism" '(1 + 1))
  (clojure.core/println "criticism" '(+ 1 1))))
```



そして、最初の `println` 呼び出しを評価すると、次のようになります。



```
(do
 (nil
  (clojure.core/println "criticism" '(+ 1 1))))
```



で、2回目の `println` 呼び出しを評価した後、次のようにします。



```
(do
 (nil nil))
```



これが例外の原因です。`println` は `nil` として評価されるので、 `(nil nil)` のようなものが出てきます。`nil` は呼び出し可能ではないので、 `NullPointerException` が発生します。

なんて不便なんだ! しかし、偶然にも、非クオートスプライシングはまさにこのような状況に対処するために発明されたのです。アンクォートのスプライシングは `~@` で行われます。単にリストをアンクォートすると、このようになります。



```
`(+ ~(list 1 2 3))
; => (clojure.core/+ (1 2 3))
```



しかし、アンクオートスプライシングを使うと、このようになります。



```
`(+ ~@(list 1 2 3))
; => (clojure.core/+ 1 2 3)
```



アンクオートスプライシングはシーケンス可能なデータ構造を解き、その内容を直接、構文クォートされたデータ構造内に配置します。これは、`~@` がハンマーで、それに続くものがピニャータであり、その結果が今までで最も恐ろしくて素晴らしいパーティになるようなものです。

とにかく、あなたのcode criticでアンクオートスプライシングを使用すれば、すべてがうまくいくでしょう。



```
(defmacro code-critic
  [{:keys [good bad]}]
  `(do ~@(map #(apply criticize-code %)
              [["Sweet lion of Zion, this is bad code:" bad]
               ["Great cow of Moscow, this is good code:" good]])))

(code-critic (1 + 1) (+ 1 1))
; => Sweet lion of Zion, this is bad code: (1 + 1)
; => Great cow of Moscow, this is good code: (+ 1 1)
```



わーーーーい 繰り返しのコードを関数に抽出し、マクロのコードをすっきりさせることに成功しましたね。ウィニペグのかわいいモルモット、これはいいコードだ!

## 気をつけたいこと

マクロには、気をつけなければならない落とし穴があります。このセクションでは、マクロの落とし穴とその回避方法について学びます。まだ、思考のマストから外れていないことを祈ります。

### 変数のキャプチャ

*変数キャプチャ*は、マクロがバインディングを導入する際に、マクロのユーザーにはわからないが、既存のバインディングを消してしまう場合に発生します。たとえば、次のコードでは、マクロがいたずらで独自の `let` バインディングを導入し、コードを台無しにしています。



```
(def message "Good job!")
(defmacro with-mischief
  [& stuff-to-do]
  (concat (list 'let ['message "Oh, big deal!"])
          stuff-to-do))

(with-mischief
  (println "Here's how I feel about that thing you did: " message))
; => Here's how I feel about that thing you did: Oh, big deal!
```



`println` コールはシンボル `message` を参照しており、これは文字列 `"Good job!"` に束縛されていると考えられます。しかし、 `with-mischief` マクロは `message` に対して新しいバインディングを作成しました。

このマクロは構文クォートを使っていないことに注意してください。そうすると、例外が発生します。



```
(def message "Good job!")
(defmacro with-mischief
  [& stuff-to-do]
  `(let [message "Oh, big deal!"]
     ~@stuff-to-do))

(with-mischief
  (println "Here's how I feel about that thing you did: " message))
; Exception: Can't let qualified name: user/message
```



この例外はあなたのためです。構文クォートはマクロの中で誤って変数をキャプチャすることを防ぐために設計されています。もし、マクロの中で `let` バインディングを導入したい場合には、 *gensym* を使用することができます。 gensym` 関数は、連続して呼び出すたびにユニークなシンボルを生成します。



```
(gensym)
; => G__655

(gensym)
; => G__658
```



また、シンボルプレフィックスを渡すことも可能である。



```
(gensym 'message)
; => message4760

(gensym 'message)
; => message4763
```



ここで、`with-mischief`をより茶目っ気のないものに書き換える方法を紹介します。



```
(defmacro without-mischief
  [& stuff-to-do]
  (let [macro-message (gensym 'message)]
    `(let [~macro-message "Oh, big deal!"]
       ~@stuff-to-do
       (println "I still need to say: " ~macro-message))))

(without-mischief
  (println "Here's how I feel about that thing you did: " message))
; => Here's how I feel about that thing you did:  Good job!
; => I still need to say:  Oh, big deal! 
```



この例では、`gensym` を使って新しいユニークなシンボルを作成し、それを `macro-message` にバインドすることで、変数の捕捉を回避しています。構文クォートされた `let` 式の中では、 `macro-message` はクォートされておらず、gensym のシンボルに解決されています。このgensymシンボルは `stuff-to-do` 内のどのシンボルとも異なるので、変数の捕捉を避けることができます。これはよくあるパターンなので、*auto-gensym* を使用することができます。 auto-gensymはgensymをより簡潔に、より便利に使うための方法です。



```
`(blarg# blarg#)
(blarg__2869__auto__ blarg__2869__auto__)

`(let [name# "Larry Potter"] name#)
; => (clojure.core/let [name__2872__auto__ "Larry Potter"] name__2872__auto__)
```



この例では、構文クォートされたリスト内のシンボルにハッシュマーク(どうしてもというなら *ハッシュタグ*)を追加することでauto-gensymを作成します。 Clojureは自動的に `x#` の各インスタンスが同じ構文クォートされたリスト内の同じシンボルに解決し、 `y#` の各インスタンスが同じように解決することを保証します。

`gensym` と auto-gensym はマクロを書くときに常に使用され、変数のキャプチャを回避することができます。

### 二重評価

マクロを書くときに気をつけなければならないもう一つの問題は、引数としてマクロに渡されたフォームが2回以上評価される*2重評価*である。次のような場合を考えてみましょう。



```
(defmacro report
  [to-try]
  `(if ~to-try
     (println (quote ~to-try) "was successful:" ~to-try)
     (println (quote ~to-try) "was not successful:" ~to-try)))
     
;; Thread/sleep takes a number of milliseconds to sleep for
(report (do (Thread/sleep 1000) (+ 1 1)))
```



このコードは、その引数が真であるかどうかをテストするためのものである。引数が真で有れば成功、偽であれば失敗と見なします。マクロはその引数が成功したかどうかを表示します。この場合、`(Thread/sleep 1000)` が2回評価されるため、実際には2秒間スリープすることになります。これは、`(do (Thread/sleep 1000) (+ 1 1))` というコードがマクロ展開の中で繰り返されているために起こります。まるで、こう書いているようなものです。



```
(if (do (Thread/sleep 1000) (+ 1 1))
  (println '(do (Thread/sleep 1000) (+ 1 1))
           "was successful:"
           (do (Thread/sleep 1000) (+ 1 1)))
  
  (println '(do (Thread/sleep 1000) (+ 1 1))
           "was not successful:"
           (do (Thread/sleep 1000) (+ 1 1))))
```



「大きなお世話だ！」と、あなたの中の例の批評家は言うでしょう。しかし、もしあなたのコードが銀行口座間の送金のようなものであれば、これは非常に大きな問題です。この問題を回避する方法を紹介しましょう。



```
(defmacro report
  [to-try]
  `(let [result# ~to-try]
     (if result#
       (println (quote ~to-try) "was successful:" result#)
       (println (quote ~to-try) "was not successful:" result#))))
```



`let` 式の中に `to-try` を置くことで、そのコードを一度だけ評価し、その結果を自動生成されたシンボル `result#` にバインドします。このシンボルは `to-try` コードを再評価することなく参照できるようになります。

### マクロの全貌

マクロを使用する際の微妙な落とし穴として、何かを成し遂げるためにどんどんマクロを書かなければならなくなることがあります。これは、マクロの展開が評価の前に行われるという事実の結果です。

例えば、`report`マクロを使用して `doseq` したいとします。 report を何度も呼び出す代わりに



```
(report (= 1 1))
; => (= 1 1) was successful: true

(report (= 1 2))
; => (= 1 2) was not successful: false
```



繰り返しましょう。



```
(doseq [code ['(= 1 1) '(= 1 2)]]
  (report code))
; => code was successful: (= 1 1)
; => code was successful: (= 1 2)
```



report マクロは関数を個別に渡すとうまく動作しますが、 `doseq` を使って `report` を複数の関数に対して繰り返し実行すると、意味のない失敗をします。以下は、 `doseq` の繰り返しの1つに対するマクロ展開の例です。



```
(if
 code
 (clojure.core/println 'code "was successful:" code)
 (clojure.core/println 'code "was not successful:" code))
```



ご覧のように、`report` は各反復で評価されていないシンボル `code` を受け取ります。しかし、評価時に `code` にバインドされているものを受け取りたいのです。しかし、マクロ展開時に動作している `report` はそれらの値にアクセスすることができません。まるで T. rex の腕を持っているようで、実行時の値には永遠に手が届かないのです。

この状況を解決するために、次のような別のマクロを記述することができます。



```
(defmacro doseq-macro
  [macroname & args]
  `(do
     ~@(map (fn [arg] (list macroname arg)) args)))

(doseq-macro report (= 1 1) (= 1 2))
; => (= 1 1) was successful: true
; => (= 1 2) was not successful: false
```



もし、このような状況に陥ったら、時間をかけて自分のやり方を考え直してください。ありふれた関数の呼び出しでは、何も達成できないのです。 そうすると、もっとマクロを書かなければならなくなります。マクロは非常に強力で素晴らしいものなので、使うことを恐れてはいけません。 Clojureのデータを扱うための機能を、あなたのプログラミングの問題から得た新しい言語を作るための機能に変えてくれます。いくつかのプログラムでは、あなたのコードの90パーセントがマクロであることが適切です。 マクロは素晴らしいものですが、新しい構成の課題も追加されます。マクロはお互いにしか合成できないので、マクロを使うことによって、Clojureで利用できる他の種類の合成（関数型、オブジェクト指向型）を見逃してしまうかもしれません。

これで、マクロを書くためのすべてのメカニックをカバーしました。自分をほめてあげてください。かなり重要なことです!

この章の最後には、いよいよ、ふりふり帽子をかぶって、この章の一番最初にお話したオンライン・ポーション・ストアで作業する時がやってきました。

## 勇気ある者、真の者に捧げるビール

![](wizard.png)

この章の冒頭で、私はある種の夢を明かしました。それは、一度飲むと一時的に80年代のフィットネス・グルーのようなパワーと気質を与え、抑制と自己認識の牢獄から私を解放してくれる飲み物を見つけることです。いつかどこかで誰かがそんな万能薬を発明してくれるに違いない。だから、この神話的な薬を売るためのシステムに取り掛かった方がいい。この仮想の調合薬を「ブレイブ・アンド・トゥルー・エール*」と呼ぶことにしましょう。この名前は、何の理由もなく思いついたものです。

注文が殺到する前に（ダジャレです！ハイタッチ！）、何らかの検証を行う必要があります。このセクションでは、このバリデーションを機能的に行う方法と、`if-valid`というマクロを使ってバリデーションを行うコードをもう少し簡潔に記述する方法を説明します。これは、自分でマクロを書くための典型的な状況を理解するのに役立つでしょう。マクロの定義だけ知りたい場合は、`"if-valid`" on page 182`まで読み飛ばしても問題ありません。

### バリデーション関数

シンプルにするために、各注文の名前とEメールのバリデーションを行うことにします。私たちのショップでは、これらの注文の詳細をこのように表現することを望んでいます。



```
(def order-details
  {:name "Mitchard Blimmons"
   :email "mitchard.blimmonsgmail.com"})
```



この特定のマップには無効なメールアドレスがあります (`@` シンボルがない) ので、これはまさに検証コードが捕捉すべき注文です! 理想的には、次のようなコードを書きたいところです。



```
(validate order-details order-details-validations)
; => {:email ["Your email address doesn't look like an email address."]}
```



つまり、検証が必要なデータと検証方法の定義を持って、関数 `validate` を呼び出すことができればよいのです。その結果は、各キーが無効なフィールドに対応し、各値がそのフィールドに対する1つまたは複数の検証メッセージのベクトルであるマップであるべきです。次の2つの関数がその仕事をします。

まず、 `order-details-validations` を見てみましょう。ここでは、バリデーションをどのように表現するかを説明します。



```
(def order-details-validations
  {:name
   ["Please enter a name" not-empty]

   :email
   ["Please enter an email address" not-empty

    "Your email address doesn't look like an email address"
    #(or (empty? %) (re-seq #"@" %))]})
```



これは、各キーにエラーメッセージと検証関数のペアのベクトルを関連付けたマップである。例えば、 `:name` は `not-empty` という1つの検証関数を持っています。もしこの検証が失敗した場合には、 `"Please enter a name"` というエラーメッセージが表示されるはずです。

次に、 `validate` 関数を書き出す必要がある。1つはバリデーションを1つのフィールドに適用する関数で、もう1つはエラーメッセージを蓄積して、 `{:email ["Your email address doesn't look like an email address."]}` のようなエラーメッセージの最終マップにする関数です。 ここでは、1つの値に対してバリデーションを適用する `error-messages-for` という関数を紹介します。



```
(defn error-messages-for
  "Return a seq of error messages"
  [to-validate message-validator-pairs]
  (map first (filter #(not ((second %) to-validate))
                     (partition 2 message-validator-pairs))))
```



最初の引数 `to-validate` は、検証したいフィールドを指定します。 2番目の引数 `message-validator-pairs` は、偶数個の要素を持つseqである必要があります。この seq は `(partition 2 message-validator-pairs)` でペアにグループ化される。ペアの最初の要素はエラーメッセージで、ペアの2番目の要素は関数であるべきです (ちょうど `order-details-validations` でペアが配置されているのと同じです)。`error-messages-for` 関数は、 `to-validate` に適用したときにバリデーション関数が `true` を返す、すべてのエラーメッセージとバリデーションのペアをフィルタリングすることで動作します。そして、 `map first` を使用して、各ペアの最初の要素であるエラーメッセージを取得します。以下はその例です。



```
(error-messages-for "" ["Please enter a name" not-empty])
; => ("Please enter a name")
```



さて、これらのエラーメッセージをマップに蓄積していく必要があります。

以下は完全な `validate` 関数と、それを `order-details` と `order-details-validations` に適用したときの出力です。



```
(defn validate
  "Returns a map with a vector of errors for each key"
  [to-validate validations]
  (reduce (fn [errors validation]
            (let [[fieldname validation-check-groups] validation
                  value (get to-validate fieldname)
                  error-messages (error-messages-for value validation-check-groups)]
              (if (empty? error-messages)
                errors
                (assoc errors fieldname error-messages))))
          {}
          validations))

(validate order-details order-details-validations)
; => {:email ("Your email address doesn't look like an email address")}
```



成功です! これは、`order-details-validations` を縮小して、`order-details` の各キーに対するエラーメッセージ (もしあれば) を関連付け、最終的にエラーメッセージのマップにすることで動作します。

### if-valid

バリデーションコードを用意すれば、あとは心ゆくまでレコードのバリデーションを行うことができます。たいていの場合、バリデーションは次のようなものになります。



```
(let [errors (validate order-details order-details-validations)]
  (if (empty? errors)
    (println :success)
    (println :failure errors)))
```



以下のようにするパターンです。

1.  レコードを検証し、その結果を `errors` にバインドする。
2.  エラーがあったかどうかをチェックする
3.  エラーがあった場合、ここでは `(println :success)` という成功処理を行います。
4.  そうでなければ、失敗の処理をします。ここでは `(println :failure errors)` です。

この検証コードを実際に本番のWebサイトで使ってみました。これは、`validate`関数を適用し、結果を何らかのシンボルにバインドし、結果が空であるかどうかをチェックするという、繰り返しの部分を隠す抽象化を導入する必要があることを示すサインです。この抽象化を実現するために、次のような関数を書きたくなるかもしれません。



```
(defn if-valid
  [record validations success-code failure-code]
  (let [errors (validate record validations)]
    (if (empty? errors)
      success-code
      failure-code)))
```



しかし、これでは `success-code` と `failure-code` が毎回評価されてしまうので、うまくいかないでしょう。マクロを使えば、評価をコントロールすることが可能になるからです。以下はマクロの使い方です。


```
(if-valid order-details order-details-validations errors
 (render :success)
 (render :failure errors))
```



このマクロは、繰り返しの詳細を隠し、より簡潔に意図を表現するのに役立ちます。"液体の入ったガラス容器から仮封材を取り除くための手動装置をください "と言う代わりに、"栓抜きをください "と言うようなものです。以下はその実施例です。



```
(defmacro if-valid
  "Handle validation more concisely"
  [to-validate validations errors-name & then-else]
  `(let [~errors-name (validate ~to-validate ~validations)]
     (if (empty? ~errors-name)
       ~@then-else)))
```



このマクロは4つの引数をとります。このマクロは、 `to-validate`, `validations`, `errors-name`, そして残りの引数 `then-else` の4つの引数を取ります。このように `errors-name` を使用するのは、新しい戦略です。私たちは `validate` 関数が返すエラーに、 `then-else` 文の中でアクセスできるようにしたいのです。 これを行うには、マクロに結果をどのシンボルにバインドするかを指示します。 次のマクロ展開は、これがどのように機能するかを示しています。



```
 (macroexpand
 '(if-valid order-details order-details-validations my-error-name
            (println :success)
            (println :failure my-error-name)))
(let*
 [my-error-name (user/validate order-details order-details-validations)]
 (if (clojure.core/empty? my-error-name)
  (println :success)
  (println :failure my-error-name)))
```



構文クォートは、先ほど見た `let/validate/if ` パターンの一般的な形式を抽象化したものです。そして、アンクオートスプライシングを使って、 `then-else` の残りの引数にパックされていた `if` 分岐をアンパックしています。

とても簡単ですね。これだけマクロについて詳しく説明しておきながら、もっと複雑なことを期待していたのではありませんか? ごめんね、友よ。もし、あなたが失望に対処するのに苦労しているのなら、私はある飲み物を知っているので、それを飲んでみてください。

## まとめ

この章では、独自のマクロを作成する方法を学びました。マクロは関数と同じように定義され、引数、docstring、本体を持ちます。マクロは引数の再構築や引数の復元が可能で、再帰的な記述も可能です。マクロはほとんどの場合リストを返します。単純なマクロでは `list` や `seq` 関数を使用することもありますが、ほとんどの場合、安全なテンプレートを使用してマクロを記述できる構文クォート `\`` を使用することになるでしょう。

マクロを書くときには、シンボルと値の区別に注意することが重要です。マクロはコードが評価される前に展開されるため、評価結果にアクセスすることはできません。二重評価や変数の取り込みも油断のならない罠ですが、`let`式やgensymをうまく使えば避けることができます。

マクロは、制限の少ないコーディングを可能にする楽しいツールです。評価をコントロールすることで、マクロは他の言語では不可能な自由度と表現力を与えてくれます。Clojureの旅を通して、「マクロは邪道だ」「マクロは絶対に使ってはいけない」などと、マクロの使用を注意する人たちを耳にすることがあると思います。少なくとも、最初のうちはそうではありません。少なくとも最初はそうだ！そこに行って、楽しい時間を過ごす。そうすれば、マクロを使うのが適切な状況を知ることができます。そうすれば、マクロを上手に、そして華麗に使いこなすことができるようになりますよ。

## 演習問題

1.  マクロ `when-valid` を書いて、`when` と同じように動作するようにしてください。 以下は、それを呼び出す例である。

    ```
    (when-valid order-details order-details-validations
     (println "It's a success!")
     (render :success))
    ```

    データが有効な場合は `println` と `render` フォームが評価され、データが無効な場合は `when-valid` が `nil` を返すようにする必要があります。
2.  `and` がマクロとして実装されていることを見ました。`or` をマクロとして実装してください。
3.  第5章では、RPGキャラクターの属性を読み取る一連の関数(`c-int`, `c-str`, `c-dex`)を作成しました。1回のマクロ呼び出しで、任意の数の属性取得関数を定義するマクロを書きましょう。以下は、その呼び出し方です。

    ```
    (defattrs c-int :intelligence
              c-str :strength
              c-dex :dexterity)
    ```

