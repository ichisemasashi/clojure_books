


Chapter 13


# マルチメソッド、プロトコル、レコードによる抽象化の作成と拡張

大自然の最高級品である人間であることがどれほど素晴らしいことか、少し考えてみよう。人間として、あなたはソーシャルメディアでゴシップを流し、ダンジョンズ＆ドラゴンズで遊び、帽子をかぶることができる。 さらに重要なのは、抽象的な考え方やコミュニケーションができることだ。

抽象的な用語で考える能力は、まさに人間の最高の特徴のひとつだ。異質な細部をきちんとした概念的なパッケージにまとめ、ワーキングメモリーに保持することで、認知の限界を回避することができる。" 搾り取れる赤い玉の鼻の装飾品 "という不便な思考をする代わりに、"ピエロの鼻 "という概念だけが必要なのだ。

Clojureでは、 *抽象化* は操作のコレクションであり、*データ型*は抽象化を実装する。例えば、seq抽象は`first`や`rest`のような操作で構成され、vectorデータ型はその抽象の実装である。また、`[:seltzer :water] `のような特定のベクトルは、そのデータ型の*インスタンス*である。

プログラミング言語が抽象的な考え方や書き方を可能にすればするほど、生産性は向上する。例えば、あるデータ構造がseq抽象のインスタンスであることを知れば、そのデータ構造でどのような関数が動作するかについて、即座に膨大な知識の網を呼び出すことができる。その結果、そのデータ構造がどのように機能するかについてのドキュメントを調べ続ける代わりに、実際にそのデータ構造を使うことに時間を費やすことができる。同じ意味で、seq抽象化で動作するようにデータ構造を拡張した場合、seq関数の広範なライブラリを使用することができます。

第4章で、Clojureは抽象化の観点から書かれていることを学びました。Clojureでは、データ構造で実際にできることに集中でき、実装の細かなことは気にしなくていいので、これは強力です。この章では、独自の抽象を作成して実装する世界を紹介します。マルチメソッド、プロトコル、レコードの基本を学びます。

## ポリモーフィズム

Clojureで抽象化を実現する主な方法は、操作名を複数のアルゴリズムに関連付けることです。このテクニックは *ポリモーフィズム* と呼ばれます。例えば、リストに対して `conj` を実行するアルゴリズムは、ベクトルに対するものとは異なりますが、同じ概念を実装していることを示すために、同じ名前で統一します。

Clojureは多くのデータ型についてJavaの標準ライブラリに依存しているので、この章では少しJavaが使われています。例えば、Clojure文字列は単なるJava文字列で、Javaクラス `java.lang.String` のインスタンスです。Javaで独自のデータ型を定義するには、クラスを使います。Clojureは追加の型構造体を提供します： *records*と*types*です。本書ではレコードのみを扱います。

レコードについて学ぶ前に、ポリモーフィックな振る舞いを定義する最初のツールであるマルチメソッドを見てみましょう。

### マルチメソッド

*マルチメソッド*は、コードにポリモーフィズムを導入するための直接的で柔軟な方法です。マルチメソッドを使うと、 *ディスパッチ関数* を定義することで、ひとつの名前に複数の実装を関連付けることができます。 ディスパッチ関数は、レストランのホストのようなものだ。ホストは、"ご予約はお決まりですか？"とか " 宴会の人数は？"といった質問をして、それに応じて席を決める。同様に、マルチメソッドを呼び出すと、ディスパッチ関数は引数を尋ねて適切なメソッドに送ります：


```
(ns were-creatures)
➊ (defmulti full-moon-behavior (fn [were-creature] (:were-type were-creature)))
➋ (defmethod full-moon-behavior :wolf
  [were-creature]
  (str (:name were-creature) " will howl and murder"))
➌ (defmethod full-moon-behavior :simmons
  [were-creature]
  (str (:name were-creature) " will encourage people and sweat to the oldies"))

(full-moon-behavior {:were-type :wolf
➍                      :name "Rachel from next door"})
; => "Rachel from next door will howl and murder"

(full-moon-behavior {:name "Andy the baker"
➎                      :were-type :simmons})
; => "Andy the baker will encourage people and sweat to the oldies"
```



このマルチメソッドは、さまざまな種類の人狼の満月の行動をどのように定義するかを示している。狼男が狼に変身して吠えながら走り回り、人々を殺害することは誰もが知っている。あまり知られていないが、リチャード・シモンズ（Richard Simmons）に変身し、パワーパーマをかけ、人々にベストを尽くすよう励まし、老人に汗を流す。どちらにも噛まれたくはないだろう、あなた自身がそうならないように。

![](weresimmons.png)

➊でマルチメソッドを作成する。これはClojureに「おい、`full-moon-behavior`という名前の新しいマルチメソッドを作れ。誰かが `full-moon-behavior` を呼び出すたびに、引数に対してディスパッチ関数 `(fn [were-creature] (:were-type were-creature))` を実行する。その関数の結果、通称ディスパッチ値を使って、どのメソッドを使うかを決めるのだ！」

次に、2つのメソッドを定義します。1つはディスパッチ関数が返す値が ➋ で `:wolf` であるときのためのメソッド、もう1つは ➌ で `:simmons` であるときのためのメソッドです。メソッド定義は関数定義とよく似ていますが、大きな違いはメソッド名の直後に *ディスパッチ値* が続くことです。`wolf` と `:simmons` はどちらも *ディスパッチ値* です。 これはディスパッチ関数が返すディスパッチ値とは異なります。完全なディスパッチシーケンスは次のようになります：

1.  フォーム `(full-moon-behavior {:were-type :wolf :name "Rachel from next door"})`が評価されます。
2.  `full-moon-behavior` のディスパッチ関数が実行され、ディスパッチ値として `:wolf` が返される。
3.  Clojureはディスパッチ値 `:wolf` と `full-moon-behavior` に定義されたすべてのメソッドのディスパッチ値を比較する。ディスパッチ値は `:wolf` と `:simmons` である。
4.  ディスパッチ値 `:wolf` はディスパッチ値 `:wolf` と等しいので、`:wolf` のアルゴリズムが実行されます。

用語に惑わされてはいけない！要は、ディスパッチ関数は何らかの値を返し、この値を使ってどのメソッド定義を使うかを決めるということです。

例に戻りましょう！次にメソッドを2回呼び出す。➍で、ディスパッチ関数は値 `:wolf` を返し、対応するメソッドが使われ、`"Rachel from next door will howl and murder"`と通知します。➏において、関数は `:simmons` がディスパッチされる値であることを除いて、同様の動作をします。

`nil`をディスパッチ値としてメソッドを定義することができます：



```
(defmethod full-moon-behavior nil
  [were-creature]
  (str (:name were-creature) " will stay at home and eat ice cream"))

(full-moon-behavior {:were-type nil
                     :name "Martin the nurse"})
; => "Martin the nurse will stay at home and eat ice cream"
```



`full-moon-behavior`を呼び出すと、引数の `:were-type` には `nil` が指定されているので、 `nil` に対応するメソッドが評価され、`"Martin the nurse will stay at home and eat ice cream"`と通知される。

また、ディスパッチ値として `:default` を指定することで、他にマッチするメソッドがない場合に使用するデフォルトのメソッドを定義することもできます。この例では、与えられた引数の `:were-type` は、以前に定義されたメソッドのどれにもマッチしないので、デフォルトのメソッドが使用されます：



```
(defmethod full-moon-behavior :default
  [were-creature]
  (str (:name were-creature) " will stay up all night fantasy footballing"))

(full-moon-behavior {:were-type :office-worker
                     :name "Jimmy from sales"})
; => "Jimmy from sales will stay up all night fantasy footballing"
```



マルチメソッドのいいところは、いつでも新しいメソッドを追加できることだ。`were-creatures`名前空間を含むライブラリを公開すれば、他の人が新しいディスパッチ値を扱うためにマルチメソッドを拡張し続けることができます。この例では、独自のランダムな名前空間を作成し、`were-creatures` 名前空間を含めて、`full-moon-behavior` マルチメソッドに別のメソッドを定義しています：



```
(ns random-namespace
  (:require [were-creatures]))
(defmethod were-creatures/full-moon-behavior :bill-murray
  [were-creature]
  (str (:name were-creature) " will be the most likeable celebrity"))
(were-creatures/full-moon-behavior {:name "Laura the intern" 
                                    :were-type :bill-murray})
; => "Laura the intern will be the most likeable celebrity"
```



ディスパッチ関数は、その引数の一部または全部を使って任意の値を返すことができます。次の例では、2つの引数を取り、それぞれの引数の型を含むベクトルを返すマルチメソッドを定義しています。また、各引数が文字列の場合に呼び出されるメソッドの実装も定義しています：



```
(ns user)
(defmulti types (fn [x y] [(class x) (class y)]))
(defmethod types [java.lang.String java.lang.String]
  [x y]
  "Two strings!")

(types "String 1" "String 2")
; => "Two strings!"
```



ちなみに、これが*マルチ*メソッドと呼ばれるゆえんである。私はこの機能をあまり使ったことがないが、ロールプレイングゲームで、たとえば魔導士の主な流派と魔法の専門性に応じてディスパッチされるメソッドを書くのに使うことができるだろう。いずれにせよ、必要なのに持っていないよりは、持っていて必要ない方がいい。

`注` マルチメソッドは `階層的ディスパッチ` も可能にします。Clojureではカスタム階層を構築することができます。ここでは取り上げませんが、 <http://clojure.org/multimethods/> にあるドキュメントを読むことでそれらについて学ぶことができます。

### プロトコル

約93.58パーセントの確率で、引数の型に応じてメソッドをディスパッチする必要があるとします。例えば、 `count` はベクトルに対してマップやリストとは異なるメソッドを使用する必要があります。 マルチメソッドでも型ディスパッチを行うことは可能ですが、 *プロトコル* は型ディスパッチに最適化されています。それらはマルチメソッドよりも効率的で、Clojureはプロトコルの実装を簡潔に指定しやすくします。

マルチメソッドは単なる1つの多相操作ですが、プロトコルは1つ以上の多相操作の *コレクション* です。プロトコル操作は、マルチメソッド操作と同じようにメソッドと呼ばれます。ディスパッチ関数が返す任意の値に対してディスパッチを行うマルチメソッドとは異なり、プロトコルメソッドは、この例に示すように、最初の引数の型に基づいてディスパッチされます：


```
(ns data-psychology)
➊(defprotocol ➋Psychodynamics
  ➌"Plumb the inner depths of your data types"
  ➍(thoughts [x] "The data type's innermost thoughts")
  ➎(feelings-about [x] [x y] "Feelings about self or other"))
```



まず、➊の `defprotocol` がある。これは `Psychodynamics` ➋ という名前と、`"Plumb the inner depths of your data types"` ➌ というオプションの docstring を受け取ります。次にメソッド・シグネチャです。*メソッド・シグネチャ*は名前、引数の指定、そしてオプションのdocstringで構成される。最初のメソッドシグネチャは `thought` ➍ という名前で、引数を1つだけ取ることができます。2つ目は `feelings-about` ➎という名前で、1つまたは2つの引数を取ることができる。プロトコルには一つの制限がある。つまり、以下のような行は許されない：



```
(feelings-about [x] [x & others])
```



プロトコルを定義するということは、抽象化を定義するということだが、その抽象化がどのように実装されるかはまだ定義していない。振る舞い（この例では`thought`と`feelings-about`）の名前を予約しているようなもので、その振る舞いが具体的にどうあるべきかは定義していない。もし`(thinks "blorb")`を評価しようとすると、"No implementation of method: thoughts of protocol: data-psychology/Psychodynamics found for class: java.lang.String. "という例外が発生するでしょう。プロトコルは最初の引数の型でディスパッチするので、`(thinks "blorb")`を呼び出すと、Clojureは文字列用の`thinks`メソッドの実装を探そうとして失敗します。

この残念な状態は、文字列データ型を *拡張* して `Psychodynamics` プロトコルを *実装* することで解決できます：



```
➊ (extend-type java.lang.String
➋   Psychodynamics
➌   (thoughts [x] (str x " thinks, 'Truly, the character defines the data type'"))
➍   (feelings-about
    ([x] (str x " is longing for a simpler way of life"))
    ([x y] (str x " is envious of " y "'s simpler way of life"))))

(thoughts "blorb")
➎ ; => "blorb thinks, 'Truly, the character defines the data type'"

(feelings-about "schmorb")
; => "schmorb is longing for a simpler way of life"

(feelings-about "schmorb" 2)
; => "schmorb is envious of 2's simpler way of life"
```



`extend-type`の後には、拡張したいクラスや型の名前とサポートさせたいプロトコルを指定する---この例では、➊でクラス `java.lang.String` を、➋でサポートさせたいプロトコル `Psychodynamics` を指定する。その後、 ➌ で `thought` メソッドと ➍ で `feelings-about` メソッドの実装を提供します。プロトコルを実装するために型を拡張する場合、プロトコルのすべてのメソッドを実装しなければ、Clojureは例外をスローします。この場合、`thought`だけ、あるいは`feelings`だけを実装することはできません。

これらのメソッドの実装は、マルチメソッドのように `defmethod` で始まらないことに注意してください。実際には、`defn`がないことを除けば、関数の定義に似ている。メソッドの実装を定義するには、`thought`のようにメソッド名で始まるフォームを書き、パラメータのベクトルとメソッド本体を指定します。これらのメソッドは関数と同じように Arity のオーバーロードが可能で、多重アリティの関数と同じように多重アリティのメソッド実装を定義します。これは ➍ の `feelings-about` の実装で見ることができます。

あなたが `java.lang.String` 型を拡張して `Psychodynamics` プロトコルを実装した後、Clojureは `(thoughts "blorb")` という呼び出しをディスパッチする方法を知っています。

マルチメソッドでやったように、デフォルトの実装を提供したい場合はどうすればいいでしょうか？そのためには、`java.lang.Object`を拡張すればいい。Java（そしてClojure）のすべての型は`java.lang.Object`の子孫だからです。オブジェクト指向プログラミングに馴染みがないためか）意味がよくわからなくても、気にしないでください。このテクニックを使って `Psychodynamics` プロトコルのデフォルトの実装を提供する方法を示します：


```
(extend-type java.lang.Object
  Psychodynamics
  (thoughts [x] "Maybe the Internet is just a vector for toxoplasmosis")
  (feelings-about
    ([x] "meh")
    ([x y] (str "meh about " y))))
  
(thoughts 3)
; => "Maybe the Internet is just a vector for toxoplasmosis"

(feelings-about 3)
; => "meh"

(feelings-about 3 "blorb")
; => "meh about blorb"
```



数値に対する `Psychodynamics` の実装を定義していないので、Clojure は `thoughts` と `feelings-about` の呼び出しを `java.lang.Object` に対して定義された実装にディスパッチする。

複数の型を拡張するために `extend-type` を複数回呼び出す代わりに、 `extend-protocol` を使用することで、一度に複数の型のプロトコル実装を定義することができます。以下は、先行するプロトコルの実装を定義する方法である：



```
(extend-protocol Psychodynamics
  java.lang.String
  (thoughts [x] "Truly, the character defines the data type")
  (feelings-about
    ([x] "longing for a simpler way of life")
    ([x y] (str "envious of " y "'s simpler way of life")))
  
  java.lang.Object
  (thoughts [x] "Maybe the Internet is just a vector for toxoplasmosis")
  (feelings-about
    ([x] "meh")
    ([x y] (str "meh about " y))))
```



このテクニックは`extend-type`を使うよりも便利かもしれない。 また、そうでないかもしれない。`extend-type`はどう感じますか？`extend-protocol`はどうですか？このソファに座って、それについて全部教えてくれ。

プロトコルのメソッドは、そのメソッドが定義されている名前空間に「属する」ことに注意することが重要です。これらの例では、`Psychodynamics` メソッドの完全修飾名は `data-psychology/thoughts` と `data-psychology/feelings-about` です。もしあなたがオブジェクト指向のバックグラウンドを持っているなら、これは奇妙に思えるかもしれません。しかしパニックになる必要はない！これは、Clojureが抽象化を優先するもう一つの方法です。この事実の1つの結果は、2つの異なるプロトコルに同じ名前のメソッドを含ませたい場合、プロトコルを異なる名前空間に置く必要があるということです。

## レコード

Clojureでは、独自のマップのようなデータ型である*レコード*を作成できます。マップのように、キーと値を関連付け、マップと同じように値を調べることができ、マップのように不変です。異なるのは、レコードに*フィールド*を指定する点だ。フィールドはデータのためのスロットであり、それを使うことは、データ構造が持つべきキーを指定するようなものである。レコードはプロトコルを実装するために拡張できるという点でもマップと異なる。

レコードを作成するには、`defrecord`を使って名前とフィールドを指定する：



```
(ns were-records)
(defrecord WereWolf [name title])
```



このレコードの名前は `WereWolf` で、フィールドは `name` と `title` です。このレコードのインスタンスは3つの方法で作成できる：



```
➊ (WereWolf. "David" "London Tourist")
; => #were_records.WereWolf{:name "David", :title "London Tourist"}

➋ (->WereWolf "Jacob" "Lead Shirt Discarder")
; => #were_records.WereWolf{:name "Jacob", :title "Lead Shirt Discarder"}

➌ (map->WereWolf {:name "Lucian" :title "CEO of Melodrama"})
; => #were_records.WereWolf{:name "Lucian", :title "CEO of Melodrama"}
```



➊で、Javaオブジェクトを作成するのと同じ方法でインスタンスを作成します。(*Interop* とは、Clojure 内でネイティブ Java 構成を操作する機能のことです)。引数はフィールド定義と同じ順序に従わなければならないことに注意してください。 これは、レコードが実際にはJavaクラスであるためです。

この➋のインスタンスは➊のインスタンスとほぼ同じに見えますが、重要な違いは `->WereWolf` が関数であるということです。レコードを作成すると、ファクトリー関数 `->RecordName` と `map->RecordName` が自動的に作成される。➌で、 `map->WereWolf` はレコード型のフィールドに対応するキーワードを持つマップを引数として受け取り、レコードを返します。

別の名前空間のレコード型を使いたい場合は、第12章のJavaクラスでやったように、それをインポートする必要がある。名前空間のダッシュをすべてアンダースコアに置き換えるように注意してください。この簡単な例では、別の名前空間にある `WereWolf` レコード型をインポートする方法を示しています：



```
(ns monster-mash
  (:import [were_records WereWolf]))
(WereWolf. "David" "London Tourist")
; => #were_records.WereWolf{:name "David", :title "London Tourist"}
```



`were_records`はダッシュではなくアンダースコアであることに注意。

マップの値を調べるのと同じようにレコードの値を調べることができ、Javaフィールドアクセスのインターオプを使うこともできる：



```
(def jacob (->WereWolf "Jacob" "Lead Shirt Discarder"))
➊ (.name jacob) 
; => "Jacob"

➋ (:name jacob) 
; => "Jacob"

➌ (get jacob :name) 
; => "Jacob"
```



最初の例である ➊ の `(.name jacob)` は Java interop を使用しており、 ➋ と ➌ の例はマップと同じように `:name` にアクセスします。

等しいかどうかをテストするとき、Clojureはすべてのフィールドが等しく、2つの比較対象が同じ型であることをチェックします：



```
➊ (= jacob (->WereWolf "Jacob" "Lead Shirt Discarder"))
; => true

➋ (= jacob (WereWolf. "David" "London Tourist"))
; => false

➌ (= jacob {:name "Jacob" :title "Lead Shirt Discarder"})
; => false
```



`jacob`と新しく作成されたレコードは同じ型であり、フィールドも等しいので、 ➊のテストは `true` を返します。フィールドが等しくないので、➋のテストは `false` を返す。➌の最後のテストは、2つの比較対象が同じ型を持っていないので、`false`を返す： `jacob` は `WereWolf` レコードであり、もう一方の引数はマップである。

マップに使える関数はレコードにも使える：



```
(assoc jacob :title "Lead Third Wheel")
; => #were_records.WereWolf{:name "Jacob", :title "Lead Third Wheel"}
```



しかし、フィールドを `dissoc` した場合、結果の型は普通のClojureマップになります； 元のレコードと同じデータ型にはなりません：



```
(dissoc jacob :title)
; => {:name "Jacob"} <- それはwere_records.WereWolfではない
```



これは少なくとも2つの理由から重要である。第一に、マップ値へのアクセスはレコード値へのアクセスよりも遅いので、ハイパフォーマンスなプログラムを構築している場合は注意が必要である。第二に、新しいレコード型を作成するときに、先ほど `extend-type` を使って型を拡張したのと同じように、それを拡張してプロトコルを実装することができる。レコードを `dissoc` して、その結果に対してプロトコルメソッドを呼び出そうとすると、レコードのプロトコルメソッドは呼び出されない。

レコードを定義するときにプロトコルを拡張する方法を以下に示します：



```
➊ (defprotocol WereCreature
➋   (full-moon-behavior [x]))

➌ (defrecord WereWolf [name title]
  WereCreature
  (full-moon-behavior [x]
    (str name " will howl and murder")))

(full-moon-behavior (map->WereWolf {:name "Lucian" :title "CEO of Melodrama"}))
; => "Lucian will howl and murder"
```



新しいプロトコル `WereCreature` ➊ を作成し、`full-moon-behavior` ➋ というメソッドを1つ持つ。➌で`defrecord`は `WereWolf` に対して `WereCreature` を実装している。`full-moon-behavior`の実装で最も興味深いのは、`name`にアクセスできることである。また、`title` やレコードに定義されているその他のフィールドにもアクセスできる。 また、 `extend-type` と `extend-protocol` を使ってレコードを拡張することもできる。

いつレコードを使い、いつマップを使うべきか？一般的に、同じフィールドを何度も使ってマップを作成しているような場合には、レコードの使用を検討すべきです。これは、そのデータセットがアプリケーションのドメイン内の情報を表していることを意味し、モデル化しようとしている概念に基づいた名前を提供した方が、コードがその目的をよりよく伝えることができます。それだけでなく、レコード・アクセスはマップ・アクセスよりもパフォーマンスが高いので、あなたのプログラムは少し効率的になります。最後に、プロトコルを使う場合はレコードを作成する必要がある。

## さらなる研究

Clojureは抽象化とデータ型を扱うためのツールを他にも提供している。 `deftype`、`reify`、`proxy`などです。さらに学びたい場合は、 *`http://clojure.org/datatypes/`* のデータ型に関するドキュメントをチェックしてください。

## まとめ

Clojureの設計原則の1つは、抽象化に対して記述することです。この章では、マルチメソッドとプロトタイプを使って独自の抽象化を定義する方法を学びました。これらの構成要素はポリモーフィズムを提供し、同じ操作が与えられた引数に基づいて異なる動作をすることを可能にします。また、`defrecord`を使って独自の関連データ型を作成して使用する方法や、プロトコルを実装するためにレコードを拡張する方法も学びました。

Clojureを学び始めた当初は、マルチメソッド、プロトコル、レコードを使うことをかなり恥ずかしがっていました。しかし、これらはClojureライブラリで頻繁に使用されるので、それらがどのように動作するかを知っておくのは良いことです。一度コツをつかめば、よりきれいなコードを書くのに役立つでしょう。

## 練習問題

1.  `full-moon-behavior`マルチメソッドを拡張して、独自のwere-creatureの振る舞いを追加してください。
2.  `WereSimmons` レコードタイプを作成し、`WereCreature` プロトコルを拡張する。
3.  独自のプロトコルを作成し、それを `extend-type` と `extend-protocol` を使って拡張する。
4.  多重ディスパッチを使って動作を実装するロールプレイングゲームを作成する。


