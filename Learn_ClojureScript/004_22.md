[UP](004_00.md)

### レッスン 22：状態の管理

このレッスンは長い時間をかけて行われてきたものであり、非常に重要なものです。ここまでで、可変型データを使用せずにアプリケーション全体を書くことが可能であることがわかりましたが、ほとんどの場合、控えめに言っても不便です。前回のレッスンで学んだように、ClojureScriptでは、純粋に関数型のコアを副作用のあるコードで囲むようにプログラムを書くことが推奨されていますが、これには状態を更新するコードも含まれます。実用的な言語であるClojureScriptは、時間とともに変化する値を扱うためのいくつかの構成要素を提供します。

-----
**このレッスンでは**

- アトムを使って時間とともに変化する値を管理する
- 状態の変化を観察して反応する
- トランジェントを使用して高性能な変異を実現する
-----

#### アトム

何度も見てきたように、ClojureScriptでは、主に不変の値を変換する純粋な関数としてプログラムを書くことが推奨されています。また、これがやや面倒であることもわかりました。ClojureScriptでは、純粋さよりも実用性を重視するという哲学に基づき、時間とともに変化する状態を表現するための便利なツール、アトムを提供しています。アトムは、任意の時点で単一の不変的な値を保持できるコンテナです。しかし、アトムが参照する値は、別の値と入れ替えることができます。さらに、このような状態の入れ替えが起こるたびに、コードはそれを観察することができます。これにより、時間とともに変化する状態を処理する便利な方法が得られます。

JavaScriptとは異なり、ClojureScriptでは、アイデンティティとステートの考え方を分けています(*1)。アイデンティティとは、論理的なエンティティへの参照です。その実体は時間の経過とともに変化することがありますが、川が時間の経過とともに異なる水が流れてもその同一性を保つのと同じように、その同一性を保ちます。アイデンティティは、時間の経過とともにさまざまな値と結びつく可能性があり、その値が状態である。アトムは、CloureScriptでアイデンティティを表現するために使用する状態コンテナです。

![state-river.png](imgs4/state-river.png)
川の状態は時間とともに変化する

##### `swap!`で状態を更新

私たちが学ぶことのできるアイデンティティの最も些細な例は、低俗なカウンタです。カウンタは、時間の経過とともに増加する数値を状態とするアイデンティティです。アトムの初期状態となる値を`v`とすると、`(atom v)`を呼び出すだけで、任意のclojureの値をアトムで包むことができます。

```Clojure
(def counter (atom 0))
```

アトムはある時点での値への参照を提供するので、`deref`マクロまたはその短縮形である`@`を使用することで、参照を解除することができます。

```Clojure
counter                                                    ;; <1>
;; => #object[cljs.core.Atom {:val 0}]

(deref counter)                                            ;; <2>
;; => 0

@counter                                                   ;; <3>
;; => 0
```
アトムの参照解除

1. アトムは、値を包むオブジェクトです。
2. アトムは`deref`を使って再参照できます。
3. アトムの名前の前に `@` を付けると、 `deref` を呼び出すことができます。

もちろん、アトムを使って何か有用なことをするためには、アトムの状態を更新できなければなりません。そのためには、`swap!`関数を使います。`swap!`はアトムと変換を受け取ります。関数はアトムの現在の状態を与えられ、その新しい状態を返さなければなりません。`swap!`自体はアトムの新しい状態を返します。`swap!`への追加の引数はすべて、変換関数への追加の引数として渡されます。単純なカウンタの場合、`inc`で増加させたり、`+`で一度に1以上の値を加えることができます。

```Clojure
(swap! counter inc)

@counter
;; => 1

(swap! counter + 9)

@counter
;; => 10
```

アトムは、状態を更新するための非常に制御されたメカニズムを提供するClojureScriptの方法です。アトムを参照解除しても、不変の値を得ることができ、アトムの状態が更新されても、受け取った値は変わりません。

```Clojure
(def creature
  (atom {:type "water"
         :life 50
         :abilities ["swimming" "speed"]}))

(def base-creature @creature)                              ;; <1>

(swap! creature update :abilities conj "night vision")

@creature                                                  ;; <2>
;; => {:type "water"
;;     :life 50
;;     :abilities ["swimming" "speed" "night vision"]}

base-creature                                              ;; <3>
;; => {:type "water"
;;     :life 50
;;     :abilities ["swimming", "speed"]}
```

1. `swap!`する前のアトムを新しい状態でデリファレンスする
2. `swap!`の後、アトムの状態は変化している
3. 取得した初期状態は変化していません

また、バリデータとして機能する関数を用意し、`set-validator!`関数(*2)を使ってアトムにどのような値が許されるかを定義することもできます。バリデーター関数は、アトムの新しい値を受け取ります。この関数が false を返す (あるいはエラーを投げる) と、更新の試みは失敗し、エラーが発生します。例えば、creatureに負の `:life` 値を設定できないことを保証するために、この特性を保証するバリデータを提供することができます。

```Clojure
(set-validator! creature
  (fn [c] (>= (:life c) 0)))

(swap! creature assoc :life 10) ;; Ok

(swap! creature assoc :life -1) ;; Throws error

(:life @creature) ;; 10
```

先ほど説明したように、バリデータが false を返すような方法でアトムの状態を更新すると、 例外が発生して更新が行われません。バリデータはClojureScriptではあまり使われませんが、関数の事前・事後条件のように、開発時には便利なツールとなります。

**クイックレビュー**

- `swap!` はどんな値を返すの？
- バリデータ関数はどのようにして、ある状態を許可すべきか否かを示すのか?

##### 状態を`reset!`で置き換える

`swap!` はアトムの状態を変換するのに便利ですが、アトムの状態全体を一度に更新したい場合もあります。ClojureScriptの標準ライブラリを使えば、これは難しいことではありません: `(swap! counter (constantly 0))`. `constantly`は、呼び出されるたびに常に特定の値を返す関数を返すので、この場合は、どんな引数が与えられても常に0を返す関数を返し、カウンターの状態を実質的に0にリセットすることになります。しかし、このコードは巧くないので、ClojureScriptでは`reset!`関数も用意しています。この関数は、単にアトムと値を受け取り、それをアトムの新しい状態として設定します。`swap!` と同様に、この関数は新しい状態を返します。

```Clojure
(reset! counter 0)

@counter
;; => 0
```

`reset!`関数は、初期状態がわかっていてそれに戻したい場合に便利ですが、そうでない場合は`swap!`関数の方がよく使われています。

##### 時計による変化の監視

アトムの最も便利な機能の一つは、状態が変化したときに通知を受けることができることです。これを実現するのが、`add-watch`関数です。この関数は3つの引数を取ります。監視するアトム、ウォッチャーを一意に識別するキーワード、そしてwatch関数です。watch関数は、`add-watch`に渡されたキーワード、アトム自身、アトムの古い状態、新しい状態を受け取ります。ほとんどの場合、古い状態と新しい状態が唯一の関心事となります。足慣らしのために、値を加減するためのボタンを備えた単純なカウンターを実装してみましょう。

![counter-swap.png](imgs4/counter-swap.png)
カウンター・アトムの監視

```Clojure
(defonce app-state (atom 0))                               ;; <1>

(def app-container (gdom/getElement "app"))

(defn render [state]                                       ;; <2>
  (set! (.-innerHTML app-container)
        (hiccups/html
          [:div
            [:p "Counter: " [:strong state]]
            [:button {:id "up"} "+"]
            [:button {:id "down"} "-"]])))

(defonce is-initialized?
  (do
    (gevents/listen (gdom/getElement "app") "click"
      (fn [e]
        (condp = (aget e "target" "id")
          "up"   (swap! app-state inc)
          "down" (swap! app-state dec))))

    (add-watch app-state :counter-observer                 ;; <3>
      (fn [key atom old-val new-val]
        (render new-val)))

    (render @app-state)

    true))
```
カウンター・コンポーネント

1. カウンタの状態を保持するアトムを作成
2. renderで現在の状態を取得
3. 状態が変化するたびにコンポーネントを再レンダリングするwatch関数を追加する

この例では、`app-state`アトムの状態の変化を監視するために`add-watch`を使用しています。関連する関数として`remove-watch`があり、これはウォッチ関数の登録を解除することができます。`remove-watch`は、監視対象のアトムと、削除するウォッチャーを示すキーワードを受け取ります。上の例でウォッチャーを削除したい場合は、次のようにこの関数を呼び出します。

```Clojure
(remove-watch app-state :counter-observer)
```

**挑戦**

[レッスン20](003_20.md)の「連絡帳」アプリを、アトムに状態を保持するようにリファクタリングしてみましょう。

#### トランジェント

アトムは時間とともに変化する状態を管理するための事実上のツールですが、トランジェントはパフォーマンスのために可変型を導入する必要がある場合に便利です。1つのデータ構造に対して多くの変換を連続して実行する必要がある場合、ClojureScriptの不変データ構造は最もパフォーマンスが高いとは言えません。不変データ構造の変換を実行するたびに、JavaScriptのガベージコレクタが掃除しなければならないゴミが発生します。このような場合には、トランジェントが非常に役立ちます。

任意のベクトル、セット、マップの一時的なバージョンは、`transient` 関数で作成できます。

```Clojure
(transient {})
;; #object[cljs.core.TransientArrayMap]
```

トランジェントを扱うAPIは、標準的なコレクションAPIと似ていますが、変換関数にはすべて`!`が付加されています（例：`assoc!`, `conj!`）。しかし、読み取りのAPIは、不変のコレクションのAPIと同じです。一時的なコレクションは、`persistent!`関数を使用して永続的なコレクションに変換することができます。

```Clojure
(-> {}
    transient                                              ;; <1>
    (assoc! :speed 12.3)
    (assoc! :position [44, 29])
    persistent!)                                           ;; <2>
```

1. マップをトランジェントに変換
2. 一時的なマップを永続的な（不変の）構造に戻す

トランジェントは一般的には使用されず、コードの一部が遅すぎることが証明された場合にのみ、パフォーマンスの最適化として考慮されるべきです。

#### 状態を賢く使う

ClojureScriptの状態管理(特にアトム)は、時間とともに変化するものをより自然に、直感的にモデル化するための大きな力を与えてくれますが、その力にはアンチパターンを導入する可能性が伴います。いくつかの簡単なガイドラインに従えば、コードを明快かつ保守的に保つことができます。

##### 指針 #1：アトムを明示的に渡す

関数をテストしやすくするためには、関数のスコープからグローバルなアトムを操作するのではなく、関数が操作するアトムを常に明示的に引数として渡す必要があります。

```Clojure
;; Don't do this
(def state (atom {:counter 0}))                            ;; <1>
(defn increment-counter []
  (swap! state update :counter inc))

;; OK
(defn increment-counter [state]                            ;; <2>
  (swap! state update :counter inc))
```

1. グローバルカウンタアトムのインクリメント
2. パラメータとして渡されたカウンタアトムをインクリメントする

どちらの関数も純粋なものではありませんが（どちらも状態を変異させる副作用があります），2番目のオプションはテスト可能で再利用可能なものです．現在のグローバルな状態に暗黙のうちに依存する必要はありません。

##### 指針 #2：アトムの数は少ないほうがいい

一般的に、アプリケーションは状態ごとに個別のアトムを持つよりも、より多くのデータを持つ少ないアトムを持つべきです。個々の状態を同期させるのではなく、アプリ全体の状態を一度に一段階ずつ遷移させることを考えた方が簡単です。

```Clojure
;; Don't do this
(def account-a (atom 100))                                 ;; <1>
(def account-b (atom 100))
(swap! account-a - 25)
(swap! account-b + 25)

;; OK
(def accounts (atom {:a 100                                ;; <2>
                     :b 100}))
(swap! accounts
  (fn [accounts]
    (-> accounts
        (update :a - 25)
        (update :b + 25))))
```

1. 「国家」の各部分を独立したアトムとして表現する
2. 我々の「世界」をひとつのアトムとして表現する

2つ目のバージョンは少し冗長ですが、「トランザクション」の一部である異なるステップ間のまとまりを作るという利点があり、多くの個別の入力に頼ることなく複雑な状態遷移を作ることができます。次のセクションで説明しますが、これはReagentフレームワークを使用する際によく見られるパターンでもあります。

#### まとめ

このレッスンでは、時間とともに変化する状態を管理するという重要な機能について紹介しました。これまで見てきたように、変更可能性に頼らなくても完全なアプリケーションを作成することができますが、制御された変更可能性をわずかに追加するだけで、コードが劇的にシンプルになります。ここでは、アトムを使用して変更可能な状態を操作する方法と、状態の変化を監視してそれに対応する方法について、ほとんどの時間を費やしました。また、トランジェント（ClojureScripts コレクションの変更可能バージョン）についても簡単に見て、パフォーマンスの最適化には適しているものの、一般的なステートコンテナとしては適していないことを学びました。最後に、アプリケーションの保守性とテスト性を高めるために、変更可能な状態の使用を制限するためのいくつかのガイドラインを見ました。

-----

1. アイデンティティとステートについての議論は、https://clojure.org/about/state を参照してください。
2. また、アトムの作成時にバリデータを指定することもできます。その場合は、`atom`の第2引数にマップを渡し、`:validator`キーでバリデータ関数を指定します。`(atom init-val {:validator validator-fn})` 


[UP](004_00.md)

