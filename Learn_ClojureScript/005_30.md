[UP](005_00.md)

### レッスン 30：キャップストーン5 - ノート

このセクションでは、ClojureScriptの知識をWebアプリケーションに応用するためのReagentフレームワークの使い方を学んできました。この最後の基礎講座では、再びプロジェクトを使って、Reagentとモジュール式アプリケーションデザインについて学んだことをまとめます。これまでの基礎講座と同様に、この講座でも、シーケンスの扱い方から状態管理や非同期通信まで、これまでに学んだことをすべて活用します。このレッスンの終わりには、メモを取るためのアプリケーションを一から作ることになります。前回のキャップストーンと同様、今回もフロントエンドのみを作成します。https://notes-api.learn-cljs.com/accounts にPOSTリクエストを発行することで得られる一連の認証情報を使って、https://notes.learn-cljs.com/api で実行されているAPIを利用することができます。

-----
**このレッスンでは**

- 柔軟なコンポーネントベースのUIを作る
- Reagentで状態管理を行う
- RESTful APIを利用する
-----

![cljs-notes-screenshot.png](imgs5/cljs-notes-screenshot.png)
CLJSノートのスクリーンショット

#### 私たちが作っているもの

このキャップストーンの動機は、必要に応じて簡単に拡張できるシンプルなメモ帳アプリケーションが欲しいという作者自身の願望から来ています。ユーザーはこのアプリケーションを使って、メモを取ったり、タグで分類したり、メモのコレクションを編集することができなければなりません。このアプリケーションのバックエンドAPIは、2つの理由から意図的に面白くないものになっています。

1. このアプリケーションのバックエンドAPIは、意図的に面白みのないものになっていますが、これには2つの理由があります。
2. このレッスンではUIの構築に重点を置いているので、APIによるノイズは少ない方が良いのです。

このアプリは、1人のユーザーが使用するように設計されており、認証や認可を必要としません。

#### 状態の管理

何を作ろうとしているのかがわかったところで、データをモデル化し、UIコンポーネントがそのデータにアクセスするために使用できるパターンを明らかにしましょう。

まずは、ノート、タグ、それらの間の関係など、UIの状態を表す基本的なモデルを作成します。データはサーバーから取得するので、データの保存方法やUIからのアクセス方法を決定する際には、サーバーのデータモデルを考慮する必要があります。ここでは、2つの主要なエンドポイントを使用します。すべてのノートを一覧表示する`/notes`と、すべてのタグを一覧表示する`/tags`です。しかし、ほとんどの現実世界のアプリでそうであるように、データはUIでの使用には理想的な形式ではないため、一般的に正規化と呼ばれるプロセスでデータを再形成します。

##### データの正規化

リレーショナルデータベース技術がもたらした主なアイデアの1つに、正規化という概念があります。正規化には技術的な定義がありますが、ここでは非公式な説明を使うことができます。正規のアプリケーション状態では、データはコピーではなく参照によって共有されるべきです。この目的のためには、ノートとタグを別々に保存し、それらの間のリンクのリストを維持することを意味します。さらに、ルックアップが効率的になるように、それらを構造化します。例えば、以下のようなAPIレスポンスを受け取ることができます。

```Clojure
[{:id 1
  :title "Books to Read"
  :content "..."
  :tags [{:id 2 :name "list"}
         {:id 3 :name "reading"}]}
 {:id 2
  :title "Groceries"
  :content "..."
  :tags [{:id 1 :name "food"}
         {:id 2 :name "list"}]}]
```

このデータ構造の最初の難点は、タグが各ノートの下に入れ子になっていることです。ノートを中心とした表示では問題ありませんが、タグを表示または編集する場合は、この構造は理想的ではありません。ノートをそのままにして、タグの別のコレクションを維持することもできます。しかし、タグを編集する際には、ノートの下に入れ子になっているタグのすべてのコピーに同じ編集を適用しなければなりません。ここでの解決策は、リレーショナル・データベース管理システムで多対多の関係を持つ場合と同じことをすることです。つまり、ノート、タグ、そしてそれらの間の関係に別々のコレクションを作成するのです。目標は、データを次のような形に変換することです。

```Clojure
{:notes                                                    ;; <1>
   {1 {:id 1
       :title "Books to Read"
       :content "..."}
    2 {:id 2
       :title "Groceries"
       :content "..."}}
 :tags
   {1 {:id 1 :name "food"}
    2 {:id 2 :name "list"}
    3 {:id 3 :name "reading"}}

 :notes-tags
   {:by-note-id                                            ;; <2>
     {1 [2 3]
      2 [1 2]}
    :by-tag-id
     {1 [2]
      2 [1 2]
      3 [1]}}}
```

1. 各エンティティは、簡単に検索できるように、IDでインデックス化されたマップに格納されます。
2. 参照は、検索しやすいように、各方向（ノート→タグ、タグ→ノート）ごとに別々のマップに格納されます。

このコードでは、重複を避けるという約束を完全には果たせていないことがわかるでしょう。各参照は事実上2回保存されています。1回は `:by-note-id` コレクションに、もう1回は `:by-tag-id` コレクションにです。しかし実際には、この重複を局所的に処理することで、ノートにタグを追加したり削除したりするのは簡単な操作になります。

このデータを再構築するためには、以下の操作を効率的に行えるようないくつかのインデックスを作成する必要があります。

1. 任意のノートやタグをIDで検索する
2. 任意のノートがあれば、それに対応するタグを検索する。
3. 任意のタグが与えられた場合、対応するノートを検索します。

タグとノートのリソースの場合、IDからリソースへのマップが必要です。各IDは一意であるため、任意のIDに対するリソースは1つしかありません。ClojureScriptの`group-by`関数は、ほぼ私たちが望むものです...しかし、完全ではありません。

```Clojure
cljs.user=> (def items [{:id 1 :title "foo"}
                        {:id 2 :title "bar"}])
#'cljs.user/items

cljs.user=> (group-by :id items)
 {1 [{:id 1, :title "foo"}],
  2 [{:id 2, :title "bar"}]}
```

`group-by`関数は、グループ関数`f`とコレクション`xs`を受け取り、`(f x)`から同じ`(f x)`を得たすべてのアイテムのベクターへのマップを返します。キーワードは一般的にグループ関数として使用され、同じキーワードのプロパティを持つすべてのアイテムがグループ化されます。各IDはそのグループに1つの要素を持つことがわかっているので、すべての値から最初の要素を取ることができます。ClojureScriptライブラリには、マップ内のすべての値を変換する関数は用意されていませんが、自明のこととして書くことができます。

```Clojure
(defn map-values [f m]
  (into {} (for [[k v] m] [k (f v)])))
```

この関数は、`for` シーケンス内包を使用して、`m`のすべてのエントリを繰り返し処理し、同じキーを持ち、`f`が適用された値を持つ別のエントリを生成します。これらのキーと値のベクターは、新しいマップに集められます。これを使って、新しいインデックス関数を書くことができます。

```Clojure
cljs.user=> (defn make-index [f coll]
              (->> coll
                   (group-by f)
                   (map-values first)))
#'cljs.user/make-index

cljs.user=> (let [items [{:id 1 :title "foo"}
                         {:id 2 :title "bar"}]]
              (make-index :id items))
{1 {:id 1, :title "foo"},
 2 {:id 2, :title "bar"}}
```

この関数は、基本的な noteとtagのインデックスには有効ですが、`:notes-tags`のインデックスを扱うには、少し異なる戦略が必要です。まず、これらはユニークなインデックスではないので、各グループには複数の要素が含まれます。さらに、これらのインデックスの値として必要なのは、IDのシーケンスのみで、ノートやタグの完全なマップではありません。そのため、各グループの要素をマッピングして、それぞれの要素から1つのプロパティを抽出する必要があります。以下のように考えてみましょう。

```Clojure
cljs.user=> (def links [{:note-id 1 :tag-id 2}
                        {:note-id 1 :tag-id 3}
                        {:note-id 2 :tag-id 1}
                        {:note-id 2 :tag-id 2}])
#'cljs.user/links

cljs.user=> (group-by :note-id links)
{1 [{:note-id 1, :tag-id 2} {:note-id 1, :tag-id 3}],
 2 [{:note-id 2, :tag-id 1} {:note-id 2, :tag-id 2}]}
```

またしても、`group-by`は我々が望むものをほとんど与えてくれます。先ほどのように各グループに関数を適用するのではなく、グループ内の各項目に関数を適用する必要があります。これは少し複雑ですが、私たちが慣れ親しんでいるシーケンス関数だけで済みます。

```Clojure
cljs.user=> (->> links
                 (group-by :note-id)
                 (map-values #(mapv :tag-id %)))           ;; <1>
{1 [2 3],
 2 [1 2]}
```

1. 各グループに対して、すべての要素に `:tag-id` 関数をマッピングし、ベクターを生成します。

各グループの変換とグループ内の各要素の変換にオプションの関数を使えるようにすることで、必要な両方のケースを処理するように`make-index`関数を修正することができます。省略可能な引数を扱う方法として、「kwargs」（keyword args）パターンがあります。関数のパラメータベクターは、`&`で終わり、その後にマップのデストラクションパターンを続けることができます。関数は、キーワードと値のペアとして解釈される0個以上の引数のペアを受け取ります。これで、最後の`make-index`関数を書くことができます。

```Clojure
(defn make-index [coll & {:keys [index-fn value-fn group-fn]
                          :or {value-fn identity
                               group-fn identity}}]
  (->> coll
       (group-by index-fn)
       (map-values #(group-fn (mapv value-fn %)))))

;; Example usage:
cljs.user=> (make-index items
                        :index-fn :id
                        :group-fn first)
{1 {:id 1, :title "foo"},
 2 {:id 2, :title "bar"}}
cljs.user=> (make-index links
                        :index-fn :note-id
                        :value-fn :tag-id)
{1 [2 3],
 2 [1 2]}
```

この関数を書いておけば、あとはすべての`:note-id`/`:tag-id`ペアを抽出する関数と、最終的なレスポンスの正規化関数を書くだけです。

```Clojure
(defn get-links [notes]
  (mapcat (fn [note]
            (for [tag (:tags note)]
              {:note-id (:id note)
               :tag-id (:id tag)}))
          notes))

(defn normalize-notes [notes]
  (let [links (get-links notes)
        notes-without-tags (mapv #(dissoc % :tags) notes)
        all-note-tags (mapcat :tags notes)]
    {:notes (make-index notes-without-tags
                        :index-fn :id
                        :group-fn first)
     :tags (make-index all-note-tags
                       :index-fn :id
                       :group-fn first)
     :notes-tags
     {:by-note-id
      (make-index links
                  :index-fn :note-id
                  :value-fn :tag-id)
      :by-tag-id
      (make-index links
                  :index-fn :tag-id
                  :value-fn :note-id)}}))
```

データの正規化が期待通りに行われたので、次は状態の管理と調整に使用するアーキテクチャに移ります。

**クイックレビュー**

- データを正規化することのメリットは何ですか？
- kwargsパターンとは何ですか？関数にオプションのパラメータを渡す他の方法はありますか？
- 正規化されたデータフォーマットがあれば、タグが入れ子になっているノートをどのように再構成できるでしょうか？

##### UIの状態

サーバーから取得するデータに加えて、さらにいくつかの状態を維持する必要があります。

```Clojure
(ns learn-cljs.notes.state
  (:require [reagent.core :as r]))

(def initial-state
  {:current-route [:home]                                  ;; <1>
   :notifications {:messages []                            ;; <2>
                   :next-id 0}
   :data {:notes {}
          :tags {}}})

(defonce app (r/atom initial-state))
```
notes/state.cljs

1. 現在のルートのためのルートパラメータ。状態がルーティングの真実の源となり、ルーティングライブラリを使ってURLと状態を同期させることになります。
2. [レッスン29](005_29.md)から採用したコンポーネントを使って表示するための通知。

この最小限の状態が、キャップストーンプロジェクトを構築するために必要なすべてです。次に、状態の更新を調整するために使用するアーキテクチャに進みましょう。

##### 調整のためのアーキテクチャ

使用するアーキテクチャは、[レッスン29](005_29.md)のコマンド/イベントパターンに従います。流れとしては以下のようになります。

1. UIは、コマンド名とオプションのペイロードを指定して、`learn-cljs.notes.command/dispatch!`関数を呼び出すことで、コマンドを発行します。
2. コマンドハンドラは、コマンドに必要な任意の副作用（APIの呼び出しを含む）を実行し、イベントバスにイベントを発することができます。
3. 状態更新関数は、イベントをリッスンし、それに応じてグローバルなアプリケーションの状態を更新します。

[レッスン29](005_29.md)からのもう一つの変更点は、メッセージングに`core.async`を使用しないことです。`core.async`はここでも機能しますが、イベントを発行する関数とイベントハンドラにディスパッチする場所が1つずつあるという単純なケースでは、過剰な機能です。

まず最初は、コマンドディスパッチャです。これは、コマンド名とオプションのコマンドペイロードを受け取り、他の関数にディスパッチして副作用を実行したりイベントを発行したりするシンプルな関数です。

```Clojure
(ns learn-cljs.notes.command
  (:require [learn-cljs.notes.events :refer [emit!]]))

(defn handle-test-hello! [name]
  (println "Hello" name)                                   ;; <1>
  (emit! :test/greeting-dispatched {:name name}))          ;; <2>

(defn dispatch!
  ([command] (dispatch! command nil))
  ([command payload]
   (js/setTimeout                                          ;; <3>
     #(case command
        :test/hello (handle-test-hello! payload)

        (js/console.error (str "Error: unhandled command: " command)))
     0))
)
```
notes/command.cljs

1. ハンドラ関数は、副作用を実行することができます。
2. また、アプリの他の部分が反応できるように、イベントを発行する必要があります。
3. イベントが処理される前にコールスタックがクリアされるように、ディスパッチャは非同期に実行されます。

UI は `_command/dispatch!` を直接呼び出してコマンドを発行できます。例えば、コンポーネントが`(notes.command/dispatch! :test/hello "world")`を呼び出すと、Hello worldというテキストがコンソールに表示されます。より多くのコマンドをサポートするために、ケース式 `indispatch!` に条件を追加し、対応するハンドラ関数を追加します。

次に、イベントを購読者に配信する役割を持つ `emit!` 関数を実装する必要があります。どんなコードでも、イベントが発行されるたびに呼び出されるリスナー関数を登録して、それに反応する機会を得ることができます。

```Clojure
(ns learn-cljs.notes.events)

(def listeners (atom []))                                  ;; <1>

(defn emit!                                                ;; <2>
  ([type] (emit! type nil))
  ([type payload]
   (doseq [listen-fn @listeners]
     (listen-fn type payload))))

(defn register-listener! [listen-fn]                       ;; <3>
  (swap! listeners conj listen-fn))
```
notes/events.cljs

1. イベントが発せられたときに通知する関数を記録しておきます。
2. イベントの種類とペイロードを指定して、各リスナー関数を連続して呼び出す。
3. 他のコードがリスナーを登録できるようにする。

なお、リスナーを宣言する際には、defonceではなくdefを使用しています。これは意図的なもので、アプリがリロードされるたびにリスナーを再登録できるようにするためです。その結果、イベントハンドラを更新したときに、変更を有効にするためにアプリを完全にリフレッシュする必要がなくなります。

最後に、イベントが発生したときにアプリの状態に必要な更新を行うリスナーを登録します。

```Clojure
(ns learn-cljs.notes.state
  (:require ;; ...
            [learn-cljs.notes.events :as events]))

;; ...

(def handlers (atom {}))

(defn register-handler! [event-type handler-fn]
  (swap! handlers assoc event-type handler-fn))

(events/register-listener!
 (fn [type payload]
   (when-let [handler-fn (get @handlers type)]
     (swap! app #(handler-fn  % payload)))))
```
notes/state.cljs

これで、コードのどこからでも、イベントが発生したときにアプリの状態を更新するイベントハンドラを登録することができます。このハンドラには、データベースの状態とイベントのペイロードが渡され、データベースの状態（おそらく更新されたもの）を返すことが期待されます。

多くのリスナーを登録できるようにイベントバスを作成しましたが、登録するのは状態更新用のリスナーだけです。コマンドディスパッチャが直接アプリの状態を更新するのではなく、なぜ余計なインダイレクトの層を設けるのでしょうか。主な理由は、イベントをログに記録したり、自動化されたバグレポートとしてサーバーに送信するために`localStorage`に保存したり、状態構造を知らないサードパートのコンポーネントと統合したりする場合に、1つの場所を指定するためです。イベントを発行する行為とアプリの状態を更新する行為を切り離すことで、初期段階ではわずかな労力で、長期的には多くの柔軟性を得ることができます。

状態管理の流れを説明します。

1. UIコンポーネントは、`command/dispatch!`を使用してコマンドをディスパッチします。
2. コマンドディスパッチャはハンドラ関数を呼び出します。ハンドラ関数はイベントを発行したり、APIコールなどの副作用を実行することができます。
3. イベントバスは、リスナーにイベントを発行します。
4. 状態リスナーは、イベントとデータベースの現在の状態を、そのイベントのために登録されているハンドラに渡すことで、イベントを処理します。
5. イベントハンドラは、イベントとデータベースの現在の状態を受け取り、更新された状態を返します。
6. 更新された状態は、それに依存しているすべてのコンポーネントに伝わり、それらは再レンダリングされます。

![coordination-architecture.png](imgs5/coordination-architecture.png)
状態の調整

#### アプリケーションの構築

このレッスンの最初の部分では、機能の「水平方向のスライス」である状態管理に焦点を当てました。状態管理はフロントエンドアプリケーションの中核をなすものなので、しっかりと設計されていることが重要です。しかし、ここでは、アプリケーションの残りの部分を構築するための「垂直方向のスライス」のアプローチを紹介します。つまり、一度に一つの機能に焦点を当て、その機能に関連するUIコンポーネント、ステートハンドラ、API関数などを開発していきます。結局のところ、現実世界のアプリケーションのほとんどは、このようにして構築されます。

最初に構築する「機能」は、レイアウトです。レイアウトは非常にシンプルで、「新規ノート」ボタンを含むヘッダー、ノートの一覧を表示するサイドバー、そしてユーザーがノートを作成・編集するメインコンテンツエリアがあります。

![layout-screenshot.png](imgs5/layout-screenshot.png)
レイアウトシェル

これらの構造のほとんどを、トップレベルの`notes.cljs`ファイルに追加します。

```Clojure
(ns learn-cljs.notes
  (:require [learn-cljs.notes.ui.header :refer [header]]
            [learn-cljs.notes.ui.main :refer [main]]
            [learn-cljs.notes.ui.sidebar :refer [sidebar]]
            [learn-cljs.notes.ui.footer :refer [footer]]
            [reagent.dom :as rdom]
            [goog.dom :as gdom]))

(defn app []
  [:div.app
   [header]
   [main]
   [sidebar]
   [footer]])

(rdom/render
 [app]
 (gdom/getElement "app"))
```
notes.cljs

`header`、`main`、`sidebar`、`footer`の各コンポーネントがまだ作成されていないので、まずはヘッダーから作成していきましょう。

```Clojure
(ns learn-cljs.notes.ui.header)

(defn header []
  [:header.page-header])
```
notes/ui/header.cljs

メインのファイルは、とりあえず似たような骨格のものにします。

```Clojure
(ns learn-cljs.notes.ui.main)

(defn main []
  [:div.main])
```
notes/ui/main.cljs

続いて、サイドバーも同じパターンで作成します。

```Clojure
(ns learn-cljs.notes.ui.sidebar)

(defn sidebar []
  [:nav.sidebar])
```
notes/ui/sidebar.cljs

次に、アプリケーションの名前を表示するだけのフッターを作成します。フッターは静的なレイアウト・コンポーネントなので、このレッスンの残りの部分ではこのコンポーネントには触れません。

```Clojure
(ns learn-cljs.notes.ui.footer)

(defn footer []
  [:footer.footer "CLJS Notes"])
```
notes/ui/footer.cljs

さて、ちょっとした仕組みができたところで、ユーザーに新しいノートを作成してもらうことから始めましょう。ヘッダーにボタンを追加して、ユーザーがノートに記入して保存できるビューに移動できるようにします。小さな機能のように見えますが、これには以下が必要です。

1. ビューの概念を含む、いくつかのUIコンポーネントの追加
2. ナビゲーションを管理するルーターの導入
3. サーバーとの通信を制御するためのAPI名前空間の作成

まず、「新しいノート」ボタンをヘッダーに追加します。ヘッダーコンポーネントでは、`ui.common`（すぐに作成します）から、1つの`button`コンポーネントを必要とします。

```Clojure
(ns learn-cljs.notes.ui.header
  (require [learn-cljs.notes.ui.common :refer [button]]))

(defn header []
  [:header.page-header
   [button "+ New Note"
     {:route-params [:create-note]                         ;; <1>
      :class "inverse"}]])
```
notes/ui/header.cljs

1. `:route-params`オプションは、リンクのターゲットを制御します。

ボタンコンポーネントを実装する前に、ちょっと寄り道してルーティングについて説明しましょう。

##### ルーティング

多くのシングルページアプリケーションと同様に、どのビューを表示するかを決定するためにURLルーティングを使用します。これは、ルーティング情報を含むアプリケーションの標準的な状態を保持するために、URLではなくステートアトムを使用するための課題です。ルーティングの状態を管理するために、イベントのソースとして[bide](https://github.com/funcool/bide)ライブラリを使用します。アプリケーションのURLが変更されると、それをルートとパラメータ（例：`:edit-note`ビューのノートID）を含む`:route/navigated`イベントとして扱います。このフローにより、アプリケーションの状態を更新する可能性のあるイベントのソースとして、ブラウザ自体を扱うことができます。このルーティング方法の結果として、リンクやボタンがルーターを呼び出すことができるようにする必要があり、それによってURLが更新され、`:route/navigated`イベントが発行されます。ありがたいことに、私たちはすでにコマンドディスパッチャの抽象化を持っているので、私たちのコンポーネントはルーティングコマンドを含むコマンドをディスパッチすることができます。

![routing-flow.png](imgs5/routing-flow.png)
ルーティング・フロー

これからルータを作成して、アプリケーションの関連部分に接続します。まず、ルーターと関連するコードを含む`routes`名前空間を作りましょう。

```Clojure
(ns learn-cljs.notes.routes
  (:require [learn-cljs.notes.events :refer [emit!]]
            [bide.core :as bide]))

(defonce router                                            ;; <1>
  (bide/router [["/" :home]
                ["/notes/new" :create-note]
                ["/notes/:note-id" :edit-note]]))

(defn navigate! [route-params]                             ;; <2>
  (apply bide/navigate! router route-params))

(defn- on-navigate [name params query]                     ;; <3>
  (emit! :route/navigated [name params query]))

(defn initialize! []                                       ;; <4>
  (bide/start! router {:default :routes/home
                       :on-navigate on-navigate}))
```
notes/routes.clj

1. ルーターを一度だけ作成する
2. コマンドディスパッチャが現在のルートを更新するために呼び出す副作用のある関数
3. ルートの変更が完了したときに実行されるコールバック
4. 起動時のルーターの初期化

次に，先ほど定義した`navigate!`関数を呼び出すコマンドをディスパッチャで公開します．

```Clojure
(ns learn-cljs.notes.command
  (:require ;; ...
            [learn-cljs.notes.routes :as routes]))

(defn handle-navigate! [route-params]
  (routes/navigate! route-params))
;; ...

(defn dispatch
  ;;...
  :route/navigate (handle-navigate! payload))
```
notes/command.cljs

ディスパッチャーを介してルータをUIに公開したので、アプリケーションの起動時にルータを初期化してみましょう。

```Clojure
(ns learn-cljs.notes
  (:require ;; ...
            [learn-cljs.notes.routes :as routes]))
;; ...
(defonce initialized?
  (do
    (routes/initialize!)
    true))
```
notes.cljs

`_routes`が評価されたときにすぐにルーターを初期化するのではなく、`_routes/initialize!`を公開している理由は、ルーターが初期化されるとすぐに`on-navigate`コールバックを呼び出すためで、イベントハンドラが登録される前にそれが起こると、ステートが更新されません。コアファイルとそのインポートがすべて評価されるまでロードを延期することで、最初のルートイベントが適切に処理されることが保証されます。次に、`:route/navigated`イベントのハンドラを作成、登録します。

```Clojure
(ns learn-cljs.notes.event-handlers.routes
  (:require [learn-cljs.notes.state :refer [register-handler!]]))

(register-handler!
 :route/navigated
 (fn [db route-params]
   (assoc db :current-route route-params)))
```
notes/event_handlers/routes.cljs

ハンドラが登録されるように、起動時にこの名前空間を評価する必要がありますので、2つのステップで処理しましょう。

1. 副作用のために、すべてのイベントハンドラの名前空間をrequireする `_event-handlers.core` を作成します。
```Clojure
(ns learn-cljs.notes.event-handlers.core
   (:require [learn-cljs.notes.event-handlers.routes]))
```
notes/event_handlers/core.cljs
2. トップレベルの `_core` 名前空間で `_event-handlers.core` をrequireします。
```Clojure
(ns learn-cljs.notes
  (:require ;; ...
            [learn-cljs.notes.event-handlers.core]))
;; ...
```
notes.cljs

すべての準備が整ったところで、`main`コンポーネントを更新して、ユーザーがどのルートを通っているかによって異なるビューを読み込むようにします。

```Clojure
(ns learn-cljs.notes.ui.main
  (:require [learn-cljs.notes.state :as state]
            [learn-cljs.notes.ui.views.home :refer [home]]))

(defn not-found []                                         ;; <1>
  [:section.hero
   [:h1.title "Page Not Found!"]])

(defn main []
  (let [[route params query] (:current-route @state/app)]  ;; <2>
    [:div.main
     (case route
       :home [home]
       [not-found])]))
```
notes/ui/main.cljs

1. アプリが未知のルートにいる場合、一般的な`not-found`コンポーネントにフォールバックする
2. 適切なビューを呼び出すために、ルートのパラメータを状態から引き出す

新しいノートを作成する機能に戻る前の最後のステップとして、上記で参照したシンプルな`home`ビューを作成します。

```Clojure
(ns learn-cljs.notes.ui.views.home)

(defn home []
  [:div.home.hero
   [:h1.title "Welcome to Notes"]
   [:h2.subtitle "Select a note or create a new one to get started."]])
```
notes/ui/views/home.cljs

ルーティングを正しく動作させるためにはかなりの努力が必要でしたが、その価値は十分にありました。今では非常にすっきりとしたルーティング・アーキテクチャになっており、ビューを簡単に追加できるだけでなく、UIコンポーネントをルーティング・メカニズムから切り離すことができます。

**課題**

ルーターを更新して、ハッシュ ベースではなく HTML5 の履歴ベースのルーティングを使用するようにします。これを Figwheel 開発サーバーで動作させるためには、サーバー側の Clojure コードを少し書く必要があります。

##### 新しいノートの作成

寄り道が長くなってしまいましたが、新しいノートを追加するためのボタンを作成している最中だったことを忘れてしまっても仕方ありません。`ui.header.cljs`ファイルの中で、`[learn-cljs.notes.ui.common :refer [button]]`というrequireを追加していましたが、これをこれから作成します。

```Clojure
(ns learn-cljs.notes.ui.common
  (:require [learn-cljs.notes.command :refer [dispatch!]]))

(defn handle-navigate [route-params]
  (fn [_]
    (dispatch! :route/navigate route-params)))

(defn button [text {:keys [route-params class]
                    :or {class ""}}]
  [:button {:class (str "button " class)
            :on-click (handle-navigate route-params)}
   text])
```
notes/ui/common.cljs

今のところ、ボタンコンポーネントはリンクのように動作していますが、これはまさに私たちが望んでいる動作です。以下は、このボタンで有効にしたルーティングの完全なフローです。

1. ボタンは、ボタンコンポーネントを使って、ペイロードとしてルートパラメータ `[:create-note]` を持つ `:route/navigate` コマンドをディスパッチします。
2. コマンドディスパッチャは、このコマンドをルータに渡し、ブラウザのURLを変更させます。
3. ルートが変更されると、ルータは新しいルートパラメータを含む `:route/navigated` イベントを発行します。
4. `event_handlers/routes.cljs`のイベントハンドラは、アプリケーションステートの`:current-route`パラメータを更新することで、このイベントに応答します。
5. 最後に、`main`コンポーネントは状態の変化によって再レンダリングされ、`:create-note`ルートに関連したビューをロードし、フォールバックとして「Not Found」ルートをレンダリングします。

> チェックポイント
>
> アプリがロードされると、トップページ、空のサイドバー、そしてヘッダーに「+ New Note」というボタンが表示されます。このボタンをクリックすると、「Page Not Found」ビューに移動します。

次に、 `:create-note` ルート用のビューを作成します。

```Clojure
(ns learn-cljs.notes.ui.views.note-form
  (:require [reagent.core :as r]
            [learn-cljs.notes.state :refer [app]]
            [learn-cljs.notes.ui.common :refer [button]]))

(defn update-data [data key]                               ;; <1>
  (fn [e]
    (swap! data assoc key (.. e -target -value))))

(defn input [data key label]                               ;; <2>
  (let [id (str "field-" (name key))]
    [:div.field
     [:div.label
      [:label {:for id} label]]
     [:div.control
      [:input {:id id
               :type "text"
               :on-change (update-data data key)
               :value (get @data key "")}]]]))

(defn textarea [data key label]
  (let [id (str "field-" (name key))]
    [:div.field
     [:div.label
      [:label {:for id} label]]
     [:div.control
      [:textarea {:id id
                  :on-change (update-data data key)
                  :value (get @data key "")}]]]))

(defn submit-button [data text]
  [button text {:dispatch [:notes/create @data]}])         ;; <3>

(defn note-form []
  (let [form-data (r/cursor app [:note-form])]             ;; <4>
    (fn []
      [:section.note-form
       [:h2.page-title "Edit Note"]
       [:form
        [input form-data :title "Title"]
        [textarea form-data :content "Content"]
        [submit-button form-data "Save"]]])))
```
notes/ui/views/noteform.cljs

1. `data`アトムに特定のキーを設定するイベントハンドラのコンストラクタ
2. inputとtextareaのヘルパーコンポーネント
3. ヘッダーで使用されているボタンコンポーネントを再利用しますが、 `:dispatch`オプションを使用します。
4. このコンポーネントが必要とする状態のみを選択するために、Reagentカーソルを使用します。

このファイルには特に目新しいものはないので、ボタンコンポーネントに戻って `:dispatch` オプションのサポートを追加してみましょう。これは、ボタンがクリックされたときに、オプションの値で指定されたコマンド名とペイロードで、コマンドディスパッチャを呼び出すことを意図しています。また、 `:on-click` オプションを追加して、単に指定されたコールバックを呼び出すようにすることもできます。

```Clojure
;; ...
(defn handle-dispatch [command-data]
  (fn [e]
    (.preventDefault e)
    (apply dispatch! command-data)))

(defn button [text {:keys [route-params dispatch on-click class]
                    :or {class ""}}]
  [:button
   {:class (str "button " class)
    :on-click (cond
                route-params (handle-navigate route-params)
                dispatch (handle-dispatch dispatch)
                on-click on-click
                :else #(js/console.error "No action provided for button"))}
   text])
```
notes/ui/common.cljs

これで、ボタンの動作は、`route-params`、`dispatch`、`on-click`のいずれのオプションが指定されているかによって変わります。`cond`はencounterの最初のtruthy節の右辺を評価するので、`route-params`が指定されたときの動作は変わらないことを覚えておいてください。ただし、`dispatch`が指定された場合は、指定された引数で`command/dispatch!`を呼び出します。

**やってみよう**

`input`コンポーネントと`textarea`コンポーネントの間には、かなりの重複があります。共通のコードを1つ以上のヘルパーにファクタリングして、DRYにしてみましょう。

次に追加しなければならないのは、`:notes/create`のコマンドハンドラです。このハンドラは、APIの関数を呼び出します（これは次に実装します）。

```Clojure
;; ...
(defn handle-create-note! [note]
  (api/create-note! note))

(defn dispatch!
  ;; ...
  (case command
    ;; ...
    :notes/create (handle-create-note! payload))
)
```

これはサーバーとの最初のやりとりなので、一貫した方法でリクエストを実行するためのユーティリティーと、UIに表示されるエラー通知を出すためのユーティリティーをいくつか使用できます。

```Clojure
(ns learn-cljs.notes.api
  (:require [learn-cljs.notes.events :refer [emit!]]
            [learn-cljs.notes.errors :as err]
            [camel-snake-kebab.core :as csk]
            [camel-snake-kebab.extras :as cske]))

(defn do-request!
  ([method path cb] (do-request! method path nil cb))
  ([method path body cb]
   (let [serialized-body (when body
                           (->> body                       ;; <1>
                                (cske/transform-keys csk/->camelCaseString)
                                (clj->js)
                                (js/JSON.stringify)))]
     (-> (js/fetch (str js/API_URL path)                   ;; <2>
                   (cond-> {:method (name method)
                            :headers {"Authorization" (str "Bearer " js/API_TOKEN)}
                            :credentials "include"}
                     (some? body)
                     (->
                      (assoc :body serialized-body)
                      (update :headers merge {"content-type" "application/json"}))

                     :always
                     clj->js))
         (.then (fn [res]
                  (if (.-ok res)
                    (when (= 200 (.-status res))
                      (.json res))
                    (throw (ex-info "API Request Failed"
                                    {:status-code (.-status res)
                                     :status (.-statusText res)}
                                    :api-failure)))))
         (.then #(->> %
                      (js->clj)                            ;; <3>
                      (cske/transform-keys csk/->kebab-case-keyword)
                      (err/ok)
                      (cb)))
         (.catch #(cb (err/error %)))))))                  ;; <4>

(defn- display-error [err]                                 ;; <5>
  (emit! :notification/added
         {:type :error
          :text (str "API Error: " (ex-message err))}))

(defn create-note! [note]                                  ;; <6>
  (do-request! :post "/notes" note
               (fn [res]
                 (->> res
                      (err/map
                       #(emit! :note/created %))
                      (err/unwrap-or display-error)))))
```
notes/api.cljs

1. リクエストボディをわかりやすいJSONに変換
2. ページからグローバル変数を読み取り、APIエンドポイントと認証情報を決定する
3. レスポンスボディをJSONからClojureScriptのデータ構造に変換する
4. 投げられたエラーをエラーオブジェクトに変換する
5. エラー通知を出すためのヘルパー
6. 少なくとも、1つのリクエストを実行するためのコードは素晴らしくシンプルになりましたよね。

このファイルにはたくさんのことが書かれていますが、その大部分は`do-request!`ヘルパーの実装に関連しています。このヘルパーが何をしているのかを簡単に見てみましょう。まず、クライアントコードがHTTPメソッド、APIベースに相対するURL、オプションのボディ、レスポンスコールバックを指定できるようにします。ボディが指定された場合、`camel-snake-kebab`ライブラリを使用して、Clojureスタイルのスネークケースのキーワードキーをキャメルケースの文字列に変換し、レスポンスボディに対してはその逆の処理を行います（`camel-snake-kebab/camel-snake-kebab {:mvn/version "0.4.2"}`をプロジェクトの依存関係に追加することを忘れないでください）。また、[レッスン24](004_24.md)で説明したエラー処理のテクニックの一部を使用して、成功またはエラーの結果をコールバックに渡します。`errors`名前空間は、[レッスン24](004_24.md)からそのまま引用していますので、ここでは繰り返しません。

いくつかのグローバル変数を読み込む必要があるので、`index.html`を開いてこれらを追加してみましょう。

```html
<!-- ... -->
<script type="text/javascript">
  window.API_URL = "https://notes-api.learn-cljs.com";
  window.API_TOKEN =
    "Get an API token with: curl -X POST https://notes-api.learn-cljs.com/accounts";
</script>
<!-- ... -->
```
index.html

使用されている通知コンポーネントは[レッスン29](005_29.md)から採用されており、ここでは明示的には取り上げません。付属のリポジトリにあるコードを参考にしてください。

新しいノートを作成する最後のステップとして、`:note/created`イベントのイベントハンドラを登録する必要があります。

```Clojure
(ns learn-cljs.notes.event-handlers.api-data
  (:require [learn-cljs.notes.state :refer [register-handler!]]
            [learn-cljs.notes.command :refer [dispatch!]]))

(register-handler!
 :note/created
 (fn [db payload]
   (let [{:keys [id title]} payload]
     (dispatch! :notification/add
                {:type :info
                 :text (str "Note created: " title)})
     (dispatch! :route/navigate                            ;; <1>
                [:edit-note {:note-id id}])
     (assoc-in db [:data :notes id]                        ;; <2>
               (dissoc payload :tags)))))
```
notes/event_handlers/apidata.cljs

1. ナビゲーションイベントをディスパッチして、このノートのエディットビューをロードします。
2. 返されたノートを、アプリケーション・ステートのノート・インデックスに挿入します。

起動時にこの名前空間が評価されるように、`notes/event_handlers/core.cljs`でこの名前空間をrequireすることを忘れないでください。

> **チェックポイント**
>
> 新しいノートフォームにタイトルと内容を入力することができ、「保存」ボタンをクリックするとノートがサーバーに保存されます。また、ノート編集用のURLにリダイレクトされるが、まだ表示はされていない。

**課題**

このキャップストーンはすでに大規模なものです。余分な課題は必要ありません。紅茶でも飲みに行きましょう。

##### ノートの表示

これまでに追加したコードに比べると、ノートのリストを追加するのは小さな作業です。新しいノートを作成する最初の機能では、UIコンポーネントから始めてAPIに戻ってきました。今回の機能ではその逆で、データをどうやってUIに取り込むかに注目し、それを表示するためのコンポーネントを作っていきましょう。

まず、「/notes」エンドポイントを呼び出してノートの全リストを取得する関数をAPIに追加します。






[UP](005_00.md)
